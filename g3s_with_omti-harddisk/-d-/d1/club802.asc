
WDR Grafik Files und HRG Model 1 Grafik Files
                     Egbert Schr|er, Februar 1993

Im Laufe der Jahre und aus guten alten Computerclub Zeiten  haben 
sich doch bei dem ein oder anderen Grafik Files angesammelt,  die 
man nicht mehr missen m|chte. Sollen die verloren gehen, nur weil 
man  den  Rechner wechselt ? Nat}rlich nicht. Im  folgenden  zwei 
Basic Programme zum Einlesen von WDR Grafik-Files und HRG-Grafik-
Files im Model 1 Format in die Grafik des Genie IIIs.
Bei  den  Grafik Files im WDR Format  sind  einige  Informationen 
verborgen,  die man zur optimalen Darstellung auf der Grafik  des 
Genie IIIs heranziehen kann. 
Dies wird in der Unterroutine "Monitor einstellen" erledigt.
Zur gef{lligen Beachtung auch die Error Routine zur Fehlerbehand-
lung.
  
100 REM ************************************
110 REM BILD 2 (LESER) *
120 REM F}R Genie IIIs  *
130 REM MIT RDL-Basic *
140 REM W.BACK MAI 85 ge{. 5/92 E.Schr|er *
150 REM ************************************
160 CLEAR 1000 : ON ERROR GOTO 65010 : CLS : HCLS : HON
170 DEFINT A-Z : DEFFNAT$(Z,S)=CHR$(28)+STRING$(Z,26)+
    STRING$(S,25) : NG$=CHR$(22)
180 GOSUB 8010 : 'Titel
190 PRINT "DATEINAME : ";:LINE INPUT NAM$
200 PRINT"LAUFWERK : ";:LINE INPUT LAUF$
210 OPEN"I" , 1 , NAM$ + ":" + LAUF$ : CLS
220 LINE INPUT #1,N$ : IF N$<>"BILD"AND N$<>"BILD" THEN 220
230 LINE INPUT #1,R$
240 LINE INPUT #1,V$ : VE% = VAL(MID$(V$,2,3))
250 LINE INPUT #1,H$ : HO% = VAL(MID$(H$,2,3))
260 LINE INPUT #1,D$ : LINE INPUT #1,D$
265 GOSUB 5010
270 FOR V = 0 TO VE%
280 IF EOF(1) THEN CLOSE : GOTO 400
290 LINE INPUT #1 , A$ : X = 1
300 FOR H = 0 TO HO% STEP 6 : X = X + 1
310 A = ASC(MID$(A$,X,1)) - 32 : IF A = 0 THEN 380
312 V2% = VE% - V
320 IF A AND 1 THEN  HPLOT H ,V2%,2
330 IF A AND 2 THEN  HPLOT H+1,V2%,2
340 IF A AND 4 THEN  HPLOT H+2,V2%,2
350 IF A AND 8 THEN  HPLOT H+3,V2%,2
360 IF A AND 16 THEN HPLOT H+4,V2%,2
370 IF A AND 32 THEN HPLOT H+5,V2%,2
380 NEXT H , V
400 IF INKEY$="" THEN 400
500 END
5000 REM Monitor einstellen
5010 IF HO% < VE% THEN 5020 ELSE 5040
5020 VE%=VE%-1 : IF HO%-VE% < 0 THEN 5020
5030 CMD"##,h" : RETURN
5040 IF HO% = VE% THEN CMD"##,v" : RETURN
5050 IF HO% > 511 AND VE% > 239 THEN CMD"##,x" : RETURN
5060 IF HO% < 512 AND VE% < 240 THEN CMD"##,h" : RETURN
5070 RETURN
8000 REM Zeichensatz laden und Programmtitel ausgeben
8010 CMD"z,y" : HCODE"script/rzs"
8020 CMD"##,h" : HCLS : Z=63 : S=6
8030 HBIG 55,220,1,2,1,"Bild - Lader"
8040 HTEXT 45,150,2,"nach einer Idee von W. Back"
8050 HTEXT 60,100,2,"WDR - Computer - Club"
8060 HCODE "normal/rzs"
8070 FOR X=0 TO 2500 : NEXT X
8080 HCLS : RETURN

6000 ' hier steht Routine "File ausw{hlen"

7000 ' hier steht Routine "Info zu WDR-File"
7010 ' Info zu Files werden ausgegeben:
       HON 0,1
       H$ -> horizontal
       V$ -> vertikal
       N$ -> Datei-Art
       R$ -> Info zu Bild (Rechner Typ)

65000 ' Error Routine
65010 IF V>VE% THEN RESUME 400
65020 IF ERR/2+1=5 THEN RESUME 380
65030 X=1
65040 FE$(1)="Fehler #"+STR$(ERR/2+1)+" in Zeile"+STR$(ERL)+
      " (bitte notieren)"
65050 FE$(2)="Eingabe : 1=nochmal, 2=weitermachen, 3=Neustart,
      4=Men}"
65060 PRINT FNAT$(Z,S);CHR$(30);NG$;FE$(X);NG$;CHR$(7);
65070 GOSUB 65080 : GOTO 65060
65080 FOR Y=1 TO 100 : X$=INKEY$ : XX=VAL(X$)
65090 IF XX>=1 AND XX <=4 THEN PRINT FNAT$(Z,S);CHR$(30);
65100 IF XX=1 THEN RESUME ELSE IF XX=2 THEN RESUME NEXT
65110 IF XX=3 THEN 160 ELSE IF XX=4 THEN 65140
65120 NEXT Y : IF X=1 THEN X=2 ELSE X=1
65130 RETURN
65140 CLOSE : RUN"menu/bas"
65150 RUN

Und  als Listing 2 das Programm zum direkten Einlesen von  Grafik 
Files im Model 1 HRG Format.

10 REM ************************************
20 REM Model 1 HRG 1b - Bildleser
30 REM f}r Genie 3s
40 REM Hartmut Obermann
45 REM ge{ndert f}r Genie 3s 05.92 E. Schr|er
50 REM ************************************
60 CLEAR 20000 : CLS :HCLS
70 DEFINT C-Z : DIMA$(191)
80 PRINT:PRINTTAB(10)"* * * * * HRG 1b - L A D E R * * * *":
   PRINT
90 PRINT"Mit diesem Programm k|nnen HRG-Bilder des Model 1 (HRG  1b)
   in die Genie 3s - HRG geladen und dann abgespeichert werden!":
   PRINT
100 PRINT "Dateiname : ";:LINE INPUT NAM$
110 PRINT"Laufwerk : ";:LINE INPUT LAUF$
120 OPEN"R" , 1 , NAM$ + ":" + LAUF$
130 FIELD 1, 64 AS B$(0), 64 AS B$(1), 64 AS B$(2), 64 AS B$(3)
140 FOR S1=0 TO 191 STEP 4 : GET 1
150 FOR S2=0 TO 3
160 A$(S1+S2)=B$(S2)
170 NEXT S2 : NEXT S1
180 CLOSE : CLS : HON
190 I=-1
200 FOR V1=11 TO 0 STEP -1
210 FOR V2=0TO191 STEP 12
220 V=240-V2+V1
230 X=0 : I=I+1
240 FOR H = 0 TO 383 STEP 6 : X = X + 1
250 A = ASC(MID$(A$(I),X,1))
260 IF A AND 1 THEN HPLOT H ,V,2
270 IF A AND 2 THEN HPLOT H+1,V,2
280 IF A AND 4 THEN HPLOT H+2,V,2
290 IF A AND 8 THEN HPLOT H+3,V,2
300 IF A AND 16 THEN HPLOT H+4,V,2
310 IF A AND 32 THEN HPLOT H+5,V,2
320 NEXT H : NEXT V2 : NEXT V1
330 HOFF : PRINT"Bild geladen!"
340 PRINT"Unter welchem Namen soll das Bild gespeichert werden?"
350 PRINT"<NAME/EXT:DNR> "; : LINEINPUT N$
360 CLS :  HON : HSAVE N$
370 HOFF : PRINT"Bild gespeichert!" : END

Das war's.

Bitte  an  alle Grafik Fan's mit MSDose: Schreibt doch  mal  eine 
Routine zum Abspeichern von Grafiken auf dem PC im WDR-Format !!

Egbert

   