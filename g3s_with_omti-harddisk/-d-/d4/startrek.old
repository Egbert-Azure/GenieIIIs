1 '*******************
2 '* SUPER STAR TREK *
3 '*     CO BY       *
4 '* EGBERT SCHROEER *
5 '* ORIG. VERS. BY  *
6 '* R.LEEDOM +      *
7 '* D.AHL           *
8 '*DORSTEN,29.03.87 *
9 '*******************
10  CLS$=CHR$(26):GOSUB 60000:'INITIALISIERUNG
11 PRINT TAB(10) A$
12 PRINT TAB(10) A4$
13 PRINT TAB(10) A2$
14 PRINT TAB(10) A4$
15 PRINT TAB(10) A$
16 PRINT:PRINT:PRINT TAB(12)"BENOETIGEN SIE ANWEISUNGEN ?  ";:GOSUB 20000:IF K$<>"J" THEN 150
17 PRINT CLS$:PRINT TAB(10)"ANWEISUNGEN FUER SUPER STAR TREK":PRINT TAB(10) A3$
18 PRINT:PRINT"1. WENN SIE 'BEFEHL ?' GEDRUCKT SEHEN, GEBEN SIE EINEN DIESER":PRINT TAB(3)"GUELTIGEN BEFEHLE EIN (NAV,SRS,LRS,PHA,TOR,SHE,DAM,COM":PRINTTAB(3)"ODER XXX)"
19 PRINT:PRINT"2. WENN SIE EINEN UNGUELTIGEN BEFEHL EINGEBEN,ERHALTEN SIE":PRINTTAB(3)"EINE KURZE LISTE DER GUELTIGEN BEFEHLE."
20 PRINT:PRINT"3. EINIGE BEFEHLE ERFORDERN DIE EINGABE VON DATEN, Z.B.":PRINTTAB(3)"'NAV' ERWIDERT MIT 'KURS (1-9) ?'. WENN SIE UNGUELTIGE DATEN,   Z.B. NEGATIVE, EINGEBEN, WIRD DER BEFEHL ABGEBROCHEN."
21 PRINT:GOSUB 20030:PRINT TAB(3)"DER WELTRAUM IST IN EIN GITTER MIT 8 * 8 QUADRANTEN AUFGE       TEILT UND JEDER QUADRANT IN 8 * 8 SEKTOREN UNTERTEILT."
22 PRINT:PRINTTAB(3)"SIE STARTEN VON EINEM BESTIMMTEN PUNKT IM WELTRAUM UND":PRINTTAB(3)"BEGINNEN IHRE LAUFBAHN ALS KOMMANDEUR DES RAUMSCHIFFES":PRINTTAB(3)"'ENTERPRISE'."
23 PRINTTAB(3)"IHR AFTRAG: DIE FLOTTE DER KLINGONEN, DIE DIE FOEDERATION"
24 PRINTTAB(3)"DER VEREINTEN PLANETEN BEDROHEN, ZU FINDEN UND"
25 PRINTTAB(3)"ZU ZERSTOEREN.":PRINT
26 PRINTTAB(3)"ALS KAPITAEN DES RAUMSCHIFFES ENTERPRISE KOENNEN SIE"
27 PRINTTAB(3)"FOLGENDE BEFEHLE EINGEBEN:"
28 PRINT
29 GOSUB 20030
30 PRINT"[NAV[ BEFEHL = MOTOREN-RICHTUNGSKONTROLLE --"
31 PRINTTAB(6)"KURS IST AUF EINER KREISFOERMIGEN     4  3  2"
32 PRINTTAB(6)"VEKTOREN-BEZEICHNUNG WIE ANGEZEIGT.    . . ."
33 PRINTTAB(6)"GANZE UND DEZIMALE WERTE KOENNEN        ..."
34 PRINTTAB(6)"VERWENDET WERDEN. (D.H. KURS 1.5    5 ---*--- 1"
35 PRINTTAB(6)"LIEGT HALB ZWISCHEN 1 UND 2)            ..."
36 PRINTTAB(45)". . ."
37 PRINTTAB(6)"DIE WERTE KOENNEN SICH BIS            6  7  8"
38 PRINTTAB(6)"AUF 9.0 NAEHERN (= 1.0)
39 PRINTTAB(47)"KURS"
40 PRINTTAB(6)"EIN RICHTUNGSFAKTOR HAT DIE GROESSE EINES"
41 PRINTTAB(6)"QUADRANTEN. DESHALB SOLLTEN SIE KURS 3, FAKTOR 1"
42 PRINTTAB(6)"BENUTZEN, UM VON QUADRANT 6,5 NACH 5,5 ZU GELANGEN."
43 GOSUB 20030
44 PRINT
45 PRINT"[SRS[ BEFEHL = KURZBEREICHS-SENSOREN UEBERPRUEFUNG"
46 PRINTTAB(6)"GIBT UEBERBLICK UEBER DEN GEGENWAERTIGEN QUADRANTEN."
47 PRINT
48 PRINTTAB(6)"SYMBOLE ERSCHEINEN AUF DEM BILDSCHIRM WIE FOLGT:"
49 PRINTTAB(10)"<*> = DIE POSITION DER 'ENTERPRISE'"
50 PRINTTAB(10)"+K+ = KLINGONEN-SCHLACHTSCHIFF"
51 PRINTTAB(10)">!< = STATION DER FOEDERATION (AUFTANKEN,REPARATUR,":PRINTTAB(17)"BEWAFFNEN)"
52 PRINTTAB(10)" *  = STERN"
53 PRINT
54 PRINTTAB(6)"EIN ZUSAMMENFASSENDER LAGEBERICHT WIRD AUCH GEGEBEN."
55 GOSUB 20030
56 PRINT
57 PRINT"[LRS[ BEFEHL = FERNBEREICHS-SENSOREN UEBERPRUEFUNG"
58 PRINTTAB(6)"ZEIGT DIE ZUSTAENDE IN DEN QUADRANTEN AUF JEDER SEITE"
59 PRINTTAB(6)"DER ENTERPRISE (DIE SICH IN DER MITTE DES BILDES BEFINDET)"
60 PRINTTAB(6)"DER BERICHT WIRD IN DER FORM '###' GEGEBEN, WOBEI DIE"
61 PRINTTAB(6)"'EINER' DIE ANZAHL DER STERNE, DIE 'ZEHNER' DIE ANZAHL"
62 PRINTTAB(6)"DER STERNENSTATIONEN UND DIE 'HUNDERTER' DIE ANZAHL"
63 PRINTTAB(6)"DER KLINGONEN SIND."
64 PRINT
65 PRINTTAB(6)"BEISPIEL - 207 = 2 KLINGONEN, KEINE STATION, 7 STERNE"
66 GOSUB 20030
67 PRINT
68 PRINT"[PHA[ BEFEHL = PHASER-KONTROLLE."
69 PRINTTAB(6)"ERMOEGLICHT IHNEN, DIE SCHLACHTSCHIFFE DER KLINGONEN"
70 PRINTTAB(6)"DURCH BESCHIESSEN MIT GENUEGEND VIEL ENERGIE ZU ZERSTOEREN"
71 PRINTTAB(6)"(BEACHTEN SIE: KLINGONEN HABEN AUCH PHASER !)
72 GOSUB 20030
73 PRINT"[TOR[ BEFEHL = PHOTONEN-TORPEDO KONTROLLE"
74 PRINTTAB(6)"DIE TORPEDO-RICHTUNG IST DIESELBE WIE BEIM STEUERN."
75 PRINTTAB(6)"WENN SIE EIN KLINGONEN-SCHIFF TREFFEN, WIRD ES"
76 PRINTTAB(6)"ZERSTOERT UND KANN NICHT AUF SIE ZURUECKFEUERN."
77 PRINTTAB(6)"WENN SIE VERFEHLEN, WERDEN SIE VON SEINEN PHASERN"
78 PRINTTAB(6)"BESCHOSSEN. IN JEDEM FALL SIND SIE DEM BESCHUSS"
79 PRINTTAB(6)"VON ANDEREN KLINGONEN IM QUADRANTEN AUSGESETZT."
80 PRINTTAB(6)"DER SPEICHER-COMPUTER ('COM'-BEFEHL) HAT DIE"
81 PRINTTAB(6)"MOEGLICHKEIT, DIE TORPEDO-FLUGBAHN FUER SIE"
82 PRINTTAB(6)"ZU BERECHNEN  (WAHL 2)"
83 GOSUB 20030
84 PRINT
85 PRINT"[SHE[ BEFEHL = SCHILD-KONTROLLE"
86 PRINTTAB(6)"BESTIMMT DIE ANZAHL DER ENERGIEEINHEITEN, DIE ZUR"
87 PRINTTAB(6)"SCHILD-SICHERUNG VERWENDET WERDEN SOLLEN, ENERGIE"
88 PRINTTAB(6)"WIRD VON DER GESAMTENERGIE DES SCHIFFES GENOMMEN"
89 PRINTTAB(6)"BEACHTEN SIE, DAS DIE GESAMTENERGIE IM LAGEBERICHT"
90 PRINTTAB(6)"DIE SCHILDENERGIE ENTHAELT."
91 GOSUB 20030
92 PRINT
93 PRINT"[DAM[ BEFEHL = ZERSTOERUNGSKONTROLLBERICHT"
94 PRINTTAB(6)"BERICHTET DEN REPARATURZUSTAND ALLER GERAETE. EIN"
95 PRINTTAB(6)"NEGATIVER 'REPARATURZUSTAND' BEDEUTET, DASS DAS"
96 PRINTTAB(6)"GERAET MOMENTAN ZERSTOERT IST."
97 GOSUB 20030
98 PRINT
99 PRINT"[COM[ BEFEHL = SPEICHER-COMPUTER"
100 PRINTTAB(6)"DER SPEICHER COMPUTER GIBT SECHS WAHLMOEGLICHKEITEN"
101 PRINTTAB(6)"WAHL 0 = ZUSAMMENFASSENDER WELTRAUMBERICHT"
102 PRINTTAB(14)"DIESE WAHL ZEIGT DIE ERGEBNISSE ALLER"
103 PRINTTAB(14)"GESPEICHERTEN KURZ-UND FERNBEREICHS"
104 PRINTTAB(14)"BERICHTE."
105 PRINTTAB(6)"WAHL 1 = LAGEBERICHT"
106 PRINTTAB(14)"DIESE WAHL ZEIGT DIE ANZAHL DER KLINGONEN,"
107 PRINTTAB(14)"STERNENDATEN UND IM SPIEL VERBLIEBENEN"
108 PRINTTAB(14)"STERNENSTATIONEN."
109 PRINTTAB(6)"WAHL 2 = DATEN FUER PHOTONEN-TORPEDO"
110 PRINTTAB(14)"DIES GIBT DIE RICHTUNG UND ENTFERNUNG VON DER"
111 PRINTTAB(14)"ENTERPRISE ZU ALLEN IM QUADRANTEN"
112 PRINTTAB(14)"BEFINDLICHEN KLINGONEN."
113 GOSUB 20030
114 PRINTTAB(6)"WAHL 3 = STERNENSTATIONS-NAV-DATEN"
115 PRINTTAB(14)"DIESE WAHL GIBT RICHTUNG UND ENTFERNUNG"
116 PRINTTAB(14)"ZU JEDER IM QUADRANTEN BEFINDLICHEN"
117 PRINTTAB(14)"STERNENSTATION."
118 PRINTTAB(6)"WAHL 4 = RICHTUNGS-UND ENTFERNUNGSRECHNER"
119 PRINTTAB(14)"DIESE WAHL ERMOEGLICHT EINGABEN ZUR"
120 PRINTTAB(14)"RICHTUNGS-UND ENTFERUNGSKALKULATION."
121 PRINTTAB(6)"WAHL 5 = KARTE DER WELTRAUM/GEBIETS-NAMEN"
122 PRINTTAB(14)"DIESE WAHL DRUCKT DIE NAMEN DER SECHZEHN"
123 PRINTTAB(14)"GROSSEN WELTRAUMGEBIETE, DIE IN DIESEM"
124 PRINTTAB(14)"SPIEL VERWENDET WERDEN."
130 GOSUB 20030
150 PRINT CLS$
151 FOR I=60 TO 87
152     SET(I,3)
153 NEXT
154 FOR I=59 TO 61
155     SET(I,4)
156     SET(I+25,4)
157 NEXT
158 FOR I=28 TO 83
159     SET(I,5)
160 NEXT
161 FOR I=54 TO 60
162     RESET(I,5)
163 NEXT
164 FOR I= 26 TO 29
165     SET(I,6)
166     SET(I+25,6)
167 NEXT: SET(69,6): SET(73,6)
168 FOR I=29 TO 50
169     SET(I,7)
170 NEXT: SET(68,7): SET(72,7)
171 FOR I=45 TO 46
172     RESET(I,7)
173 NEXT: SET(44,8): SET(46,8): SET(67,8): SET(71,8)
174 FOR I=38 TO 77
175     SET(I,9)
176     SET(I,12)
177 NEXT: RESET(44,9): RESET(45,9)
178 FOR I=67 TO 69
179     RESET(I,9)
180 NEXT
181 FOR I=39 TO 40
182     SET(I,10):SET(I+38,10)
183     SET(I,11):SET(I+38,11)
184 NEXT
185 PRINT @ 448,TAB(15)"DIE USS  ENTERPRISE --- NC 1701"
186 PRINTTAB(15)"READY FOR PLAY==>PRESS <SPACE>":GOSUB 20030
260 CLEAR 600
270 Z$="                         "
300 RANDOM
370 T=RND(4000): T0=T: T9=RND(35): D0=0: E=3000: E0=E
440 P=10: P0=P: S9=200: S=0: B9=0: K9=0: X$="": X0$=" IST "
480 RANDOM
490 'LEGE AUSGANGSLAGE DER ENTERPRISE FEST
500 Q1=RND(8): Q2=RND(8): S1=RND(8): S2=RND(8)
530 FOR I=1 TO 9: C(I,1)=0: C(I,2)=0: NEXT
540 C(3,1)=-1: C(2,1)=-1: C(4,1)=-1: C(4,2)=-1:C(5,2)=-1: C(6,2)=-1
600 C(1,2)=1: C(2,2)=1: C(6,1)=1: C(7,1)=1: C(8,1)=1: C(8,2)=1: C(9,2)=1
670 FOR I=1 TO 8: D(I)=0: NEXT
710 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
810 'AUFSTELLEN,WAS SICH IM WELTRAUM BEFINDEN WIRD
815 'K3=ANZAHL DER KLINGONEN,B3=ANZAHL DER STATIONEN,S3=ANZAHL DER STERNE
820 FOR I=1 TO 8: FOR J=1 TO 8: K3=0: Z(I,J)=0: RANDOM: R1=RND(100)
850 IF R1>98 THEN K3=3: K9=K9+3: GOTO 980
860 IF R1>95 THEN K3=2: K9=K9+2: GOTO 980
870 IF R1>80 THEN K3=1: K9=K9+1
980 B3=0: IF R1>96 THEN B3=1: B9=B9+1
1040 G(I,J)=K3*100+B3*10+RND(8): NEXT J,I: IF K9>T9 THEN T9=K9+1
1100 IF B9<>0 THEN 1200
1150 IF G(Q1,Q2)<200 THEN G(Q1,Q2)=G(Q1,Q2)+100: K9=K9+1
1160 B9=1: G(Q1,Q2)=G(Q1,Q2)+10:RANDOM: Q1=RND(9): Q2=RND(9)
1200 K7=K9: IF B9<>1 THEN X$="S": X0$=" SIND "
1230 PRINT"SIE HABEN DIE FOLGENDEN ANWEISUNGEN:"
1240 PRINT"    ZERSTOEREN SIE DIE";K9;"KLINGONEN KAMPFSCHIFFE, DIE DEN WELT-"
1250 PRINT"    RAUM AUFRUEHREN, BEVOR SIE DAS HAUPTQUATIER DER"
1260 PRINT"    FOEDERATION AM STERNENTAG";T0+T9;"ANGREIFEN KOENNEN."
1270 PRINT"    ES BLEIBEN IHNEN";T9;"TAGE."
1280 PRINT"    ES";X0$;B9;"STATIONEN IM RAUM,"
1290 PRINT"    AN DENEN SIE DAS SCHIFF AUFLADEN KOENNEN. WENN"
1292 PRINT"    SIE BEREIT SIND, DRUECKEN SIE EINE TASTE."
1300 I$=INKEY$: IF I$="" THEN 1300
1310 'IN NEUEN QUADRANT EINGEDRUNGEN
1320 RANDOM: Z4=Q1: Z5=Q2: K3=0: B3=0: S3=0: G5=0: D4=.5*RND(0): Z(Q1,Q2)=G(Q1,Q2)
1390 IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN 1600
1430 GOSUB 9030: PRINT: IF T0<>T THEN 1490
1460 PRINT"ZU BEGINN DER MISSION BEFINDET SICH DAS RAUMSCHIFF IM"
1470 PRINT"QUADRANTEN ";G2$;".": GOTO 1500
1490 PRINT"EINTRITT IN QUADRANTEN ";G2$". . ."
1500 PRINT: K3=INT(G(Q1,Q2)*.01): B3=INT(G(Q1,Q2)*.1)-10*K3
1540 S3=G(Q1,Q2)-100*K3-10*B3: IF K3=0 THEN 1590
1560 PRINT"KAMPFBEREICH    ZUSTAND ROT": IF S>200 THEN 1590
1580 PRINT"   ABWEHRSCHILDE GEFAEHRLICH NIEDRIG"
1590 FOR I=1 TO 3: K(I,1)=0: K(I,2)=0: NEXT
1600 FOR I=1 TO 3: K(I,3)=0: NEXT: Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
1660 'POSITIONIERE ENTERPRISE IM QUADRANTEN, DANACH K3 B3 S3
1680 A$="<*>":Z1=S1:Z2=S2:GOSUB 8670: IF K3<1 THEN 1820
1720 FOR I=1 TO K3:GOSUB 8590: A$="+K+":Z1=R1:Z2=R2
1780 GOSUB 8670: K(I,1)=R1: K(I,2)=R2: K(I,3)=S9*(.5+RND(0)): NEXT
1820 IF B3<1 THEN 1910
1880 GOSUB 8590: A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB 8670
1910 FOR I=1 TO S3:GOSUB 8590: A$=" * ":Z1=R1:Z2=R2:GOSUB 8670:NEXT
1980 GOSUB 6430
1990 IF S+E>10 THEN IF E>10 OR D(7)=0 THEN 2060
2020 PRINT:PRINT"**ERNSTHAFTER FEHLER ** SIE SIND GERADE MIT DEM SCHIFF"
2030 PRINT"IM RAUM GESTRANDET":PRINT"SIE HABEN UNGENUEGENDE MANOEVRIERENERGIE":PRINT"UND DIE SCHILD-KONTROLLE IST GEGENWAERTIG UNFAEHIG,"
2050 PRINT"ZUM MASCHINENRAUM UMZUSCHALTEN!!!":GOTO 6220
2060 INPUT"BEFEHL ";A$
2080 FOR I=1 TO 9: IF LEFT$(A$,3)<>MID$(A1$,3*I-2,3) THEN 2160
2140 ON I GOTO 2300,1980,4000,4260,4700,5530,5690,7290,6270
2160 NEXT:PRINT"TIPPE EINEN DER FOLGENDEN BEFEHLE:"
2180 PRINT"  NAV (FUER RICHTUNGSAENDERUNG)"
2190 PRINT"  SRS (FUER NAHBEREICHSUEBERSICHT)"
2200 PRINT"  LRS (FUER FERNBEREICHS-UEBERSICHT)"
2210 PRINT"  PHA (UM PHASER ABZUFEUERN)
2220 PRINT"  TOR (UM PHOTONEN-TORPEDOS ABZUFEUERN)"
2230 PRINT"  SHE (UM SCHILDE ZU HEBEN ODER ZU SENKEN)"
2240 PRINT"  DAM (FUER ZERSTOERUNGSBERICHT)"
2250 PRINT"  COM (UM DEN SPEICHER COMPUTER ZU BEFRAGEN)"
2260 PRINT"  XXX ( UM BEFEHL ABZUBRECHEN)": GOTO 1990
2290 'KURSKONTROLLE BEGINNT HIER
2300 INPUT"KURS (1-9)";C1: IF C1=9 THEN C1=1
2310 IF C1>=1 AND C1<9 THEN 2350
2330 PRINT"     LT. SULU BERICHTET: 'FALSCHE KURSDATEN'": GOTO 1990
2350 X$="8": IF D(1)<0 THEN X$="0.2"
2360 PRINT"FAKTOR (0 - ";X$;")";:INPUT W1: IF D(1)<0 AND W1>.2 THEN 2470
2380 IF W1>0 AND W1<=8 THEN 2490
2390 IF W1=0 THEN 1990
2420 PRINT"     CHEF-INGENIEUR KOCH BERICHTET: 'DIE MASCHINEN VERKRAFTEN"
2430 PRINT"     FAKTOR";W1;"NICHT !'": GOTO 1990
2470 PRINT"     RICHTUNGSMOTOREN SIND ZERSTOERT. HOECHSTGESCHW.= 0.2": GOTO 1990
2490 N=INT(W1*8+.5): IF E-N>=0 THEN 2590
2500 PRINT"MASCHINISTEN BERICHTEN:  'UNGENUEGEND ENERGIE, UM MIT"
2510 PRINT"             GESCHW.";W1;"ZU MANOEVRIEREN."
2530 IF S<N-E OR D(7)<0 THEN 1990
2550 PRINT"ABSCHIRM-KONTROLLRAUM BESTAETIGT: ";S;"ENERGIEEINHEITEN"
2560 PRINT"                                  FUER SCHILDE VERWENDET"
2570 GOTO 1990
2580 'KLINGONEN ZIEHEN/SCHIESSEN GEGEN ENTERPRISE
2590 RANDOM:FOR I=1 TO K3: IF K(I,3)=0 THEN 2700
2610 A$="   ":Z1=K(I,1):Z2=K(I,2): GOSUB 8670: GOSUB 8590
2660 K(I,1)=Z1: K(I,2)=Z2: A$="+K+": GOSUB 8670
2700 NEXT:GOSUB 6000:D1=0:D6=W1:IFW1>=1 THEN D6=1
2770 FOR I=1 TO 8: IF D(1)>=0 THEN 2880
2790 D(I)=D(I)+D6: IF D(I)>-.1 AND D(I)<0 THEN D(I)=-.1: GOTO 2880
2800 IF D(I)<0 THEN 2880
2810 IF D1<>1 THEN D1=1:PRINT"ZERSTOERUNGSBERICHT:  ";
2840 PRINTTAB(8);: R1=I: GOSUB 8790: PRINT G2$;" REPARATUR FERTIG."
2880 NEXT: IF RND(0)>.2>2 THEN 3070
2910 R1=RND(8): IF RND(0)>=.6 THEN 3000
2930 D(R1)=D(R1)-(RND(6)): PRINT"ZERSTOERUNGSBERICHT:   ";
2960 GOSUB 8790: PRINT G2$;" ZERSTOERT":PRINT:GOTO 3070
3000 D(R1)=D(R1)+(RND(4)):PRINT"ZERSTOERUNGSBERICHT:   ";
3030 GOSUB 8790: PRINT G2$;" REPARATURZUSTAND VERBESSERT":PRINT
3060 'ENTERPRISE BEGINNT SICH ZU BEWEGEN
3070 A$="   ":Z1=INT(S1): Z2=INT(S2):GOSUB 8670
3110 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)): X=S1:Y=S2
3140 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)): Q4=Q1: Q5=Q2
3170 FOR I=1 TO N: S1=S1+X1: S2=S2+X2: IF S1<1 OR S1>=9 OR S2<1 OR S2>=9 THEN 3500
3240 S8=INT(S1)*24+INT(S2)*3-26: IF MID$(Q$,S8,2)="  " THEN 3360
3320 S1=INT(S1-X1): S2=INT(S2-X2): PRINT"LENKMOTOREN AUSGESCHALTET IN";
3350 PRINT" SEKTOR";S1;",";"WEGEN SCHLECHTER":PRINT"NAVIGATION": GOTO 3370
3360 NEXT: S1=INT(S1): S2=INT(S2)
3370 A$="<*>":Z1=INT(S1): Z2=INT(S2): GOSUB 8670: GOSUB 3910: T8=1
3430 IF W1<1 THEN T8=.1*INT(10*W1)
3450 T=T+T8: IF T>T0+T9 THEN 6220
3470 'UEBERPRUEFE OB GESTRANDET, DANN FRAGE NACH BEFEHL
3480 GOTO 1980
3490 'QUADRANTEN-GRENZEN UEBERSCHRITTEN
3500 X=8*Q1+X+N*X1: Y=8*Q2+Y+N*X2: Q1=INT(X/8): Q2=INT(Y/8): S1=INT(X-Q1*8)
3550 S2=INT(Y-Q2*8): IF S1=0 THEN Q1=Q1-1: S1=8
3590 IF S2=0 THEN Q2=Q2-1: S2=8
3620 X5=0:IF Q1<1 THEN X5=1:Q1=1:S1=1
3670 IF Q1>8 THEN X5=1: Q1=8: S1=8
3710 IF Q2<1 THEN X5=1:Q2=1:S2=1
3750 IF Q2>8 THEN X5=1:Q2=8:S2=8
3790 IF X5=0 THEN 3860
3800 PRINT"LT. UHURA BERICHTET MELDUNG VOM FLOTTEN-KOMMANDO:"
3810 PRINT"  'ERLAUBNIS ZUM VERSUCH DIE AEUSSEREN GRENZEN DES WELTALLS":PRINT"  ZU DURCHBRECHEN, WIRD HIERMIT"
3820 PRINT"          *** VERWEIGERT ***. SCHALTE MOTOREN AUS"
3830 PRINT"CHEF-INGENIEUR KOCH BERICHTET: 'LENKMOTOREN AUSGESCHALTET"
3840 PRINT"  AUF SEKTOR";S1;",";S2;"DES QUADRANTEN";Q1;",";Q2;" ."
3850 IF T>T0+T9 THEN 6220
3860 IF 8*Q1+Q2=8*Q4+Q5 THEN 3370
3870 T=T+1: GOSUB 3910: GOTO 1320
3900 'MANOEVRIERENERGIE
3910 E=E-N-10: IF E>=0 THEN RETURN
3930 PRINT"SCHILD KONTROLLE GIBT ENERGIE AB, UM MANOEVER ABZUSCHLIESSEN"
3940 S=S+E: E=0: IF S<=0 THEN S=0
3980 RETURN
3990 'FERNBEREICHSUEBERSICHT
4000 IF D(3)<0 THEN PRINT"FERNBEREICHSSENSOREN SIND NICHT BETRIEBSFAEHIG": GOTO 1990
4030 PRINT"FERNBEREICHS-UEBERSICHT FUER QUADRANT";Q1;",";Q2
4040 O1$="-------------------":PRINT O1$
4060 FOR I=Q1-1 TO Q1+1: N(1)=-1: N(2)=-2: N(3)=-3: FOR J=Q2-1 TO Q2+1
4120 IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
4180 NEXT J: FOR L=1 TO 3: PRINT": ";: IF N(L)<0 THEN PRINT"*** ";: GOTO 4230
4210 PRINT RIGHT$(STR$(N(L)+1000),3);" ";
4230 NEXT L: PRINT":": PRINT O1$: NEXT I: GOTO 1990
4250 'PHASER KONTROLLE HIER
4260 IF D(4)<0 THEN PRINT"PHASER NICHT BETRIEBSFAEHIG": GOTO 1990
4265 IF K3>0 THEN 4330
4270 PRINT"TECHNISCHER OFFIZIER SPOCK BErICHTET: 'FUEHLER ZEIGEN KEINE"
4280 PRINT"               FEINDSCHIFFE IM QUADRANTEN'": GOTO 1990
4330 IF D(8)<0 THEN PRINT"COMPUTER-FEHLER VERFAELSCHT GENAUIGKEIT"
4350 PRINT"PHASER AUF ZIEL EINGESTELLT;   ";
4360 PRINT"VERFUEGBARE ENERGIE = ";E;"EINHEITEN"
4370 INPUT"WIEVIELE EINHEITEN ABSCHIESSEN";X: IF X<=0 THEN 1990
4400 IF E-X<0 THEN 4360
4410 E=E-X: IF D(7)<0 THEN X=RND(X)
4440 H1=INT(X/K3): FOR I=1 TO 3: IF K(I,3)<=0 THEN 4670
4480 H=INT((H1/(SQR((K(I,1)-S1)[2+(K(I,2)-S2)[2))*RND(0)+2)): IFH>.15*K(I,3) THEN 4530
4500 PRINT"FUEHLER BERICHTEN KEINE BESCHAEDIGUNG DES FEINDES AUF";K(I,1);",";K(I,2): GOTO 4670
4530 K(I,3)=K(I,3)-H: PRINT"KLINGON AUF SEKTOR";K(I,1);",";K(I,2);"MIT";H;"EIN-"
4550 PRINT"HEITEN GETROFFEN": IF K(I,3)<=0 THEN PRINT"*** KLINGON ZERSTOERT ***": GOTO 4580
4560 PRINT"   (FUEHLER ZEIGEN: NOCH";K(I,3);"EINHEITEN UEBRIG)": GOTO 4670
4580 K3=K3-1: K9=K9-1: Z1=K(I,1): Z2=K(I,2): A$="   ": GOSUB 8670
4650 K(I,3)=0: G(Q1,Q2)=G(Q1,Q2)-100: Z(Q1,Q2)=G(Q1,Q2): IF K9<=0 THEN 6370
4670 NEXT I: GOSUB 6000: GOTO 1990
4690 'PHOTONEN-TORPEDO
4700 IF P<=0 THEN PRINT"ALLE PHOTONEN-TORPEDOS VERBRAUCHT": GOTO 1990
4730 IF D(5)<0 THEN PRINT"PHOTONEN-ROEHREN SIND AUSSER BETRIEB": GOTO 1990
4760 INPUT"KURS DES PHOTONEN-TORPEDOS (1-9)";C1: IF C1=9 THEN C1=1
4780 IF C1>=1 AND C1<9 THEN 4850
4790 PRINT"FAEHNRICH CHEKOV BERICHTET: 'FALSCHE KURS-DATEN, SIR!'"
4800 GOTO 1990
4850 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)): E=E-2: P=P-1
4860 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)): X=S1: Y=S2
4910 PRINT"TORPEDO-BAHN:"
4920 X=X+X1: Y=Y+X2: X3=INT(X+.5): Y3=INT(Y+.5)
4960 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN 5490
5000 PRINT"               ";X3;",";Y3: A$="   ": Z1=X: Z2=Y: GOSUB 8830
5050 IF Z3<>0 THEN 4920
5060 A$="+K+": Z1=X: Z2=Y: GOSUB 8830: IF Z3=0 THEN 5210
5110 PRINT"*** KLINGON ZERSTOERT ***": K3=K3-1: K9=K9-1: IF K9<=0 THEN 6370
5150 FOR I=1 TO 3: IF X3=K(I,1) AND Y3=K(I,2) THEN 5190
5180 NEXT I: I=3
5190 K(I,3)=0: GOTO 5430
5210 A$=" * ": Z1=X: Z2=Y: GOSUB 8830: IF Z3=0 THEN 5280
5260 PRINT"STERN AUF";X3;",";Y3;"ABSORBIERTE TORPEDO-ENERGIE.": GOSUB 6000: GOTO 1990
5280 A$=">!<": Z1=X: Z2=Y: GOSUB 8830: IF Z3=0 THEN 4760
5330 PRINT"*** STERNENSTATION ZERSTOERT ***": B3=B3-1: B9=B9-1
5360 IF B9>0 OR K9>T-T0-T9 THEN 5400
5370 PRINT"SO GESCHIEHT ES, KAPITAEN!! DAMIT VERLIEREN SIE DAS KOMMANDO"
5380 PRINT"UND WERDEN ZU 99 STERNENTAGEN HARTER ARBEIT AUF CYGNUS STRAFVERSETZT !!"
5390 GOTO 6270
5400 PRINT"FLOTTENKOMMANDO UEBERPRUEFT IHRE DATEN FUER DAS":PRINT"MILITAERGERICHT !!"
5410 D0=0
5430 Z1=X: Z2=Y: A$="   ": GOSUB 8670
5440 G(Q1,Q2)= K3*100+B3*10+S3: Z(Q1,Q2)=G(Q1,Q2): GOSUB 6000: GOTO 1990
5490 PRINT"TORPEDO VERFEHLTE": GOSUB 6000: GOTO 1990
5520 'SCHILD-KONTROLLE
5530 IF D(7)<0 THEN PRINT"SCHILD-KONTROLLE AUSSER BETRIEB": GOTO 1990
5560 PRINT"VERFUEGBARE ENERGIE =";E+S;: INPUT"WIEVIEL EINHEITEN FUER SCHILDE";X
5580 IF X=<0 OR S=X THEN PRINT"(SCHILDE UNVERAENDERT)": GOTO 1990
5590 IF X<=E+S THEN 5630
5600 PRINT"SCHILD-KONTROLLE BERICHTET: 'DAS IST NICHT DAS FINANZAMT":PRINT"                             DER FOEDERATION"
5610 PRINT"(SCHILDE UNVERAENDERT)": GOTO 1990
5630 E=E+S-X: S=X: PRINT"ABSCHIRM-KONTROLLRAUM BERICHTET:"
5660 PRINT"   'SCHILDE BENOETIGEN JETZT";INT(S);"EINHEITEN PRO BEFEHL.'": GOTO 1990
5680 'ZERSTOERUNGSUEBERWACHUNG
5690 IF D(6)>=0 THEN 5910
5700 PRINT"ZERSTOERUNGS-UEBERSICHT NICHT VERFUEGBAR": IF D=0 THEN 1990
5720 D3=0: FOR I=1 TO 8: IF D(I)<0 THEN D3=D3+.1
5760 NEXT: IF D3=0 THEN 1990
5780 PRINT: D3=D3+D4: IF D3>=1 THEN D3=.9
5810 PRINT"TECHNIKER SIND FUER REPARATUREN DES RAUMSCHIFFES ABRUFBEREIT"
5820 PRINT"GESCHAETZTE REPARATURDAUER:";.01*INT(100*D3);"STERNENTAGE"
5840 PRINT"GENEHMIGEN SIE DIE REPARATUR ";:GOSUB 20000: IF K$<>"J" THEN 1990
5870 FOR I=1 TO 8: IF D(I)<0 THEN D(I)=0
5890 NEXT: T=T+D3+.1
5910 PRINT:PRINT"GERAET                    REPARATURZUSTAND": FOR R1=1 TO 8
5920 GOSUB 8790: PRINT G2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
5950 NEXT: PRINT: IF D0<>0 THEN 5720
5980 GOTO 1990
5990 'KLINGONEN SCHIESSEN
6000 IF K3<=0 THEN RETURN
6010 IF D0<>0 THEN PRINT"SCHILDE DER STATION SCHUETZEN DIE ENTERPRISE": RETURN
6040 FOR I=1 TO 3: IF K(I,3)<=0 THEN 6200
6060 H=INT((K(I,3)/(SQR((K(I,1)-S1)[2+(K(I,2)-S2)[2)))*(2+RND(0))): S=S-H: K(I,3)=K(I,3)/(3+RND(0))
6080 PRINT"EINHEIT";H;"WURDE VOM SEKTOR";K(I,1);",";K(I,2);"GETROFFEN"
6090 IF S<=0 THEN 6240
6100 PRINT"      (SCHILDE AUF";S;"EINHEITEN VERMINDERT)": IF H<20 THEN 6200
6120 IF RND(0)>.6 OR H/S<=.02 THEN 6200
6140 R1=RND(8): D(R1)=D(R1)-H/S-.5*RND(0): GOSUB 8790
6170 PRINT"ZERSTOERUNGS-KONTROLLE BERICHTET: '";G2$;" DURCH TREFFER BESCHAEDIGT'"
6200 RETURN
6220 PRINT"AM STERNENTAG";T: GOTO 6270
6240 PRINT:PRINT"DIE ENTERPRISE WURDE ZERSTOERT. DIE FOEDERATION"
6250 PRINT"WIRD EROBERT": GOTO 6220
6270 PRINT"AM ENDE DER MISSION SIND NOCH";K9;"SCHLACHTSCHIFFE DER"
6280 PRINT"KLINGONEN UEBRIG."
6290 PRINT:PRINT: IF B9=0 THEN 6360
6310 PRINT"DIE FOEDERATION BENOETIGT FUER EINE AEHNLICHE AUFGABE EINEN"
6320 PRINT"RAUMSCHIFF-KOMMANDANTEN. - WENN EIN FREIWILLIGER ANWESEND"
6330 PRINT"IST, SOLL ER VORTRETEN UND SICH MELDEN  ";:GOSUB 20000: IF K$="J" THEN RUN
6340 PRINT"***** SCHADE ***** ES GIBT KEINE GUTEN KOMMANDANTEN MEHR"
6350 PRINT TAB(40)"* TSCHUESS *": GOSUB 20030: CLS: END
6360 GOTO 6310
6370 PRINT"GRATULIERE KAPITAEN ! DAS LETZTE SCHLACHTSCHIFF DER KLINGONEN,"
6380 PRINT"DAS DIE FOEDERATION BEDROHTE, WURDE ZERSTOERT.":PRINT
6400 PRINT"DEINE ERFOLGSBEWERTUNG IST:";1000*(K7/(T-T0))[2: GOTO 6290
6420 'KURZBEREICHS-ABSUCHGERAETE UND BEGINN UNTERPROGRAMM
6430 FOR I=S1-1 TO S1+1: FOR J=S2-1 TO S2+1
6450 IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN 6540
6490 A$=">!<": Z1=I: Z2=J: GOSUB 8830: IF Z3=1 THEN 6580
6540 NEXT J,I: D0=0: GOTO 6650
6580 D0=1: C$="ANGELEGT": E=E0: P=P0
6620 PRINT"SCHILDE FUER ANLEGEMANOEVER GESENKT": S=0: GOTO 6720
6650 IF K3>0 THEN C$="*ROT*": GOTO 6720
6660 C$="GRUEN": IF E<E0*.1 THEN C$="GELB"
6720 IF D(2)>=0 THEN 6770
6730 PRINT:PRINT"*** KURZBEREICHSFUEHLER AUSGEFALLEN ***":PRINT:RETURN
6770 O1$="----------------------------------":PRINT O1$: FOR I=1 TO 8
6820 FOR J=((I-1)*24+1) TO ((I-1)*24+22) STEP 3: PRINT" ";MID$(Q$,J,3);:NEXT J
6830 ON I GOTO 6850,6900,6960,7020,7070,7120,7180,7240
6850 PRINT TAB(38)"STERNENTAG        ";INT(T*10)*.1: GOTO 7260
6900 PRINT TAB(38)"ZUSTAND           ";C$: GOTO 7260
6960 PRINT TAB(38)"QUADRANT          ";Q1;",";Q2: GOTO 7260
7020 PRINT TAB(38)"SEKTOR            ";S1;",";S2: GOTO 7260
7070 PRINT TAB(38)"PHOTONEN-TORPEDOS";INT(P): GOTO 7260
7120 PRINT TAB(38)"GESAMT-ENERGIE   ";INT(E+S): GOTO 7260
7180 PRINT TAB(38)"SCHILDE          ";INT(S): GOTO 7260
7240 PRINT TAB(38)"KLINGONEN UEBRIG";INT(K9)
7260 NEXT I:PRINT O1$: RETURN
7280 'SPEICHER-COMPUTER-CODE
7290 IF D(8)<0 THEN PRINT"COMPUTER AUSSER BETRIEB":GOTO 1990
7320 INPUT"COMPUTER AKTIV UND WARTET AUF BEFEHL";A: IF A<0 THEN 1990
7350 PRINT:H8=1: ON A+1 GOTO 7540,7900,8070,8500,8150,7400
7360 PRINT"VERFUEGBARE FUNKTIONEN DES SPEICHER-COMPUTERS:"
7370 PRINT"   0 = ZUSAMMENFASSENDE WELTRAUM-UEBERSICHT"
7372 PRINT"   1= ZUSTANDS-BERICHT"
7374 PRINT"   2 = PHOTONEN-TORPEDO-DATEN"
7376 PRINT"   3 = STERNENSTATIONS-NAV-DATEN"
7378 PRINT"   4 = RICHTUNGS/ENTFERNUNGS-RECHNER"
7380 PRINT"   5 = 'BEREICHS-NAMEN' - STERNENKARTE":PRINT:GOTO 7320
7390 'AUFSTELLUNG DER STERNENKARTE
7400 H8=0: G5=1: PRINT"                        WELTRAUM": GOTO 7550
7530 'ZUSAMMENFASSENDER WELTRAUM-BERICHT
7540 PRINT"MOECHTEN SIE EINEN AUSDRUCK ? IST DER DRUCKER EINGESCHALTET ?";: GOSUB 20000
7542 IF K$="J" THEN PRINT=LPRINT
7543 PRINT:PRINT"        ";
7544 PRINT"COMPUTER-WELTRAUMBERICHT FUER QUADRANT";Q1;",";Q2
7546 PRINT
7550 PRINT"        1      2      3      4      5      6      7      8"
7560 O1$="      -----  -----  -----  -----  -----  -----  -----  -----"
7570 PRINT O1$: FOR I=1 TO 8: PRINT I;: IF H8=0 THEN 7740
7630 FOR J=1 TO 8:PRINT"    ";: IF Z(I,J)=0 THEN PRINT"***";: GOTO 7720
7700 PRINT RIGHT$(STR$(Z(I,J)+1000),3);
7720 NEXTJ:GOTO 7850
7740 Z4=I: Z5=1: GOSUB 9030: J0=INT(15-.5*LEN(G2$)): PRINT TAB(J0);G2$;
7800 Z5=5: GOSUB 9030: J0=INT(39-.5*LEN(G2$)): PRINT TAB(J0);G2$;
7850 PRINT:PRINT O1$:IF K$="N" THEN GOSUB 20030: NEXT I:IF K$="N" THEN  GOSUB 20030: GOTO 1990
7890 'ZUSTANDSBERICHT
7900 PRINT"   ZUSTANDS-BERICHT:":X$="": IF K9>1 THEN X$="EN"
7940 PRINT"KLINGON";X$" UEBRIG: ";K9
7960 PRINT"AUFTRAG MUSS IN";.1*INT(T0+T9-T)*10;"STERNENTAGEN BEENDET SEIN"
7970 X$="EN": IF B9<2 THEN X$="": IF B9<1 THEN 8010
7980 PRINT"DIE FOEDERATION UNTERHAELT";B9;"STERNENSTATION";X$;" IM WELTRAUM"
7990 GOTO 5690
8010 PRINT"WEGEN DEINER DUMMHEIT VERBLEIBST DU ALLEIN IM"
8020 PRINT"  WELTRAUM -- ES IST KEINE STERNENSTATION UEBRIG!": GOTO 5690
8060 'TORPEDO,STATIONS-NAV,R/E-RECHNER
8070 IF K3<=0 THEN 4270
8080 X$="": IF K3>1 THEN X$="EN"
8090 PRINT"VON DER ENTERPRISE ZU KLINGONEN-SCHLACHTSCHIFF";X$
8100 H8=0: FOR I=1 TO 3: IF K(I,3)<=0 THEN 8480
8110 W1=K(I,1): X=K(I,2)
8120 C1=S1: A=S2: GOTO 8220
8150 PRINT"RICHTUNGS/ENTFERNUNGS-RECHNER:"
8160 PRINT"SIE SIND IM QUADRANTEN ";Q1;",";Q2;"  SEKTOR ";S1;",";S2
8170 PRINT"BITTE ANFANGSKOORDINATEN EINGEBEN (X,Y)";:INPUT C1,A
8200 PRINT"BITTE ENDKOORDINATEN EINGEBEN (X,Y)";:INPUT W1,X
8220 X=X-A: A=C1-W1: IF X<0 THEN 8350
8250 IF A<0 THEN 8410
8260 IF X>0 THEN 8280
8270 IF A=0 THEN C1=5: GOTO 8290
8280 C1=1
8290 IF ABS(A)<=ABS(X) THEN 8330
8310 PRINT"RICHTUNG =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)): GOTO 8460
8330 PRINT"RICHTUNG =";C1+(ABS(A)/ABS(X)): GOTO 8460
8350 IF A>0 THEN C1=3: GOTO 8420
8360 IF X<>0 THEN C1=5: GOTO 8290
8410 C1=7
8420 IF ABS(A)>=ABS(X) THEN 8450
8430 PRINT"RICHTUNG =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)): GOTO 8460
8450 PRINT"RICHTUNG =";C1+(ABS(X)/ABS(A))
8460 PRINT"ENTFERNUNG =";SQR(X[2+A[2): IF H8=1 THEN 1990
8480 NEXT I: GOTO 1990
8500 IF B3<>0 THEN PRINT"VON DER ENTERPRISE ZUR STERNENSTATION:": W1=B4: X=B5: GOTO 8120
8510 PRINT"MSTR. SPOCK BERICHTET:  'FUEHLER ZEIGT KEINE STERNENSTATION"
8520 PRINT"                         IN DIESEM QUADRANTEN'": GOTO 1990
8580 'FINDE LEEREN RAUM IM QUADRANTEN(FUER DINGE)
8590 RANDOM:R1=RND(8): R2=RND(8): A$="   ": Z1=R1: Z2=R2: GOSUB 8830: IF Z3=0 THEN 8600
8600 RETURN
8660 'GIB QUADRANTEN-DATEN IN FELD EIN
8670 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
8675 IF LEN(A$)<>3 THEN PRINT"FEHLER":STOP
8680 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
8690 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$: RETURN
8700 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8): RETURN
8780 'DRUCKE GERAETE-NAMEN
8790 ON R1 GOTO 8792,8794,8796,8798,8800,8802,8804,8806
8792 G2$="RICHTUNGS-MOTOREN": RETURN
8794 G2$="NAHBEREICHS-FUEHLER": RETURN
8796 G2$="FERNBEREICHS-FUEHLER": RETURN
8798 G2$="PHASER-KONTROLLE": RETURN
8800 G2$="PHOTONEN-ROEHREN": RETURN
8802 G2$="ZERSTOERUNGS-KONTROLLE": RETURN
8804 G2$="SCHILD-KONTROLLE": RETURN
8806 G2$="SPEICHER-COMPUTER": RETURN
8820 'ZEICHENFOLGEEN-VERGLEICH IM QUADRANTENFELD
8830 Z1=INT(Z1+.5): Z2=INT(Z2+.5): S8=(Z2-1)*3+(Z1-1)*24+1: Z3=0
8890 IF MID$(Q$,S8,3)<>A$ THEN RETURN
8900 Z3=1: RETURN
9010 'QUADRANTEN-NAMEN IN G2$ VON Z4,Z5(=Q1,Q2)
9020 'MIT G5=1 UM NUR GEBIETSNAMEN ZU ERHALTEN
9030 IF Z5<=4 THEN ON Z4 GOTO 9040,9050,9060,9070,9080,9090,9100,9110
9035 GOTO 9120
9040 G2$="ANTARES": GOTO 9210
9050 G2$="RIGEL": GOTO 9210
9060 G2$="PROCYON": GOTO 9210
9070 G2$="VEGA": GOTO 9210
9080 G2$="CANOPUS": GOTO 9210
9090 G2$="ALTAIR": GOTO 9210
9100 G2$="SAGITTARIUS": GOTO 9210
9110 G2$="POLLUX": GOTO 9210
9120 ON Z4 GOTO 9130,9140,9150,9160,9170,9180,9190,9200
9130 G2$="SIRIUS": GOTO 9210
9140 G2$="DENEB: GOTO 9210
9150 G2$="CAPELLA": GOTO 9210
9160 G2$="BETEIGEUZE": GOTO 9210
9170 G2$="ALDEBARAN": GOTO 9210
9180 G2$="REGULUS": GOTO 9210
9190 G2$="ARTURUS": GOTO 9210
9200 G2$="SPICA"
9210 IF G5<>1 THEN ON Z5 GOTO 9230,9240,9250,9260,9230,9240,9250,9260
9220 RETURN
9230 G2$=G2$+" I": RETURN
9240 G2$=G2$+" II": RETURN
9250 G2$=G2$+" III": RETURN
9260 G2$=G2$+" IV": RETURN
9270 END
20000 PRINT"J/N"
20010 K$=INKEY$: IF K$="" THEN 20010
20020 RETURN
20030 K$=INKEY$:IF K$="" THEN 20030 
20040 IF K$<>"J" THEN K$="N"
20050 RETURN
60000 '
60010 A$="**************************************"
60020 A4$="*                                    *"
60025 Z$="                          "
60030 A2$="*  * *  S U P E R  STAR TREK  * *    *"
60035 A3$="================================"
60040 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
60060 RETURN
 TREK  * *    *"
60035 A3$="=====