10 '***********************************************
20 '* SUPER STAR TREK  * Diese Version fuer CP/M  *
30 '*     CO BY        * (c) by E.Schroeer        *
40 '* EGBERT SCHROEER  *                          *
50 '* ORIG. VERS. BY   *	 		  *
60 '* R.LEEDOM +       *			  *	
70 '* D.AHL            *			  *
80 '* DORSTEN,29.03.87 *	Dorsten,30.01.94  *
90 '***********************************************
100 '
120 ' Diese Version ist auf Genie IIIs Zeichensatz eingestellt.
130 ' Bitte nehmen Sie die entsprechenden Anpassungen in der
140 ' Unterroutine "Initilisierungen" vor !
160 '
170 CLEAR 600
180 '
190 ' Initialisierung
200 '
210 GOSUB 6900:PRINT CLS$:PRINT TAB(10) A$
220 PRINT TAB(10)A4$:PRINT TAB(10)A2$:PRINT TAB(10)A4$:PRINT TAB(10)A$
270 PRINT CLS$:PRINT TAB(10)"Anweisungen f}r SUPER STAR TREK":PRINT TAB(10) A3$
280 PRINT:PRINT"1. Wenn Sie 'BEFEHL ?' gedruckt sehen, geben Sie einen dieser":PRINT TAB(3)"g}ltigen Befehle ein (NAV,SRS,LRS,PHA,TOR,SHE,DAM,COM":PRINT TAB(3)"oder XXX)"
290 PRINT:PRINT"2. Wenn Sie einen ung}ltigen Befehl eingeben, erhalten Sie":PRINT TAB(3)"eine kurze Liste der g}ltigen Befehle."
300 PRINT:PRINT"3. Einige Befehle erfordern die Eingabe von Daten, z.B.":PRINT TAB(3)"'NAV' erwidert mit 'KURS (1-9) ?'. Wenn Sie ungueltige Daten,":PRINT TAB(3)"z.B. negative, eingeben, wird der Befehl abgebrochen."
310 PRINT:GOSUB 6820:PRINT CLS$
320 PRINT TAB(3)"Der Weltraum ist in ein Gitter mit 8 * 8 Quadranten aufgeteilt"
330 PRINT TAB(3)"und jeder Quadrant in 8 * 8 Sektoren unterteilt.":PRINT
350 PRINT TAB(3)"Sie starten von einem bestimmten Punkt im Weltraum und"
360 PRINT TAB(3)"beginnen Ihre Laufbahn als Kommandeur des Raumschiffs ";
370 PRINT EIN$;:PRINT"ENTERPRISE";:PRINT AUS$;:PRINT" .":PRINT:PRINT
380 PRINT TAB(3)"Ihr Auftrag :":PRINT
390 PRINT TAB(10)"Die Flotte der Klingonen, die die F|deration"
400 PRINT TAB(10)"der vereinten Planeten bedrohen, zu finden und"
410 PRINT TAB(10)"zu zerst|ren.":PRINT:GOSUB 6820:PRINT CLS$
440 PRINT TAB(10)"Als Kapit{n des Raumschiffs ENTERPRISE k|nnen Sie"
450 PRINT TAB(10)"folgende Befehle eingeben:":PRINT
470 PRINT EIN$;"NAV";AUS$;" BEFEHL = MOTOREN-RICHTUNGSKONTROLLE --":PRINT:PRINT
490 PRINT TAB(10)"Der Kurs ist auf einer kreisf|rmigen      4  3  2"
500 PRINT TAB(10)"Vektoren-Bezeichnung wie angezeigt.        . . ."
510 PRINT TAB(10)"Ganze und dezimale Werte k|nnen             ..."
520 PRINT TAB(10)"verwendet werden ( d.h. Kurs 1.5        5 ---*--- 1"
530 PRINT TAB(10)"liegt halb zwischen 1 und 2)                ..."
540 PRINT TAB(53)". . ."
550 PRINT TAB(10)"Die Werte k|nnen sich bis                 6  7  8"
560 PRINT TAB(10)"auf 9.0 n{hern (= 1.0)"
570 PRINT TAB(54) EIN$;"Kurs";AUS$:PRINT
590 PRINT TAB(6)"Ein Richtungsfaktor hat die Gr|~e eines Quadranten."
600 PRINT TAB(6)"Deshalb sollten Sie Kurs 3, Faktor 1 benutzen,"
610 PRINT TAB(6)"um von Quadrant 6,5 nach 5,5 zu gelangen.":GOSUB 6820:PRINT CLS$
640 PRINT EIN$;"SRS";AUS$;" BEFEHL = KURZBEREICHS-SENSOREN ]BERPR]FUNG":PRINT:PRINT
660 PRINT TAB(10)"Gibt einen ]berblick }ber den gegenw{rtigen Quadranten.":PRINT
680 PRINT TAB(10)"Die Symbole erscheinen auf dem Bildschirm wie folgt:"
690 PRINT TAB(14)"<*> = Die Position der ";EIN$;"ENTERPRISE";AUS$
700 PRINT TAB(14)"+K+ = Klingonen-Schlachtschiff"
710 PRINT TAB(14)">!< = Station der F|deration (Auftanken,Reparatur,"
720 PRINT TAB(21)"Bewaffnen)"
730 PRINT TAB(14)" *  = Stern":PRINT
750 PRINT TAB(6)"Ein zusammenfassender Lagebericht wird auch gegeben.":GOSUB 6820:PRINT CLS$
780 PRINT EIN$;"LRS";AUS$;" BEFEHL = FERNBEREICHS-SENSOREN ]BERPR]FUNG":PRINT:PRINT
800 PRINT TAB(10)"Zeigt die Zust{nde in den Quadranten auf jeder Seite"
810 PRINT TAB(10)"der ";EIN$;"ENTERPRISE";AUS$;" (die sich in der Mitte des Bildes befindet)"
820 PRINT TAB(10)"Der Bericht wird in der Form '###' gegeben, wobei die"
830 PRINT TAB(10)"'EINER' die Anzahl der Sterne, die 'ZEHNER' die Anzahl"
840 PRINT TAB(10)"der Sternenstationen und die 'HUNDERTER' die Anzahl"
850 PRINT TAB(10)"der Klingonen sind.":PRINT
870 PRINT TAB(6)"Beispiel ";HAND$;"  207 = 2 Klingonen, keine Station, 7 Sterne":GOSUB 6820:PRINT CLS$
900 PRINT EIN$;"PHA";AUS$;" BEFEHL = PHASER-KONTROLLE.":PRINT:PRINT
920 PRINT TAB(10)"Erm|glicht Ihnen, die Schlachtschiffe der Klingonen"
930 PRINT TAB(10)"durch Beschiessen mit gen}gend viel Energie zu zerst|ren"
940 PRINT TAB(10)"(Beachten Sie: Klingonen haben auch Phaser !)":GOSUB 6820:PRINT CLS$
970 PRINT EIN$;"TOR";AUS$;" BEFEHL = PHOTONEN-TORPEDO KONTROLLE":PRINT:PRINT
990 PRINT TAB(10)"Die Torpedo-Richtung ist dieselbe wie beim Steuern."
1000 PRINT TAB(10)"Wenn Sie ein Klingonen-Schiff gen}gend stark treffen,"
1010 PRINT TAB(10)"Wird es zerst|rt und kann nicht auf Sie zur}ckfeuern."
1020 PRINT TAB(10)"Wenn Sie verfehlen, werden Sie von seinen Phasern"
1030 PRINT TAB(10)"beschossen. In jedem Fall sind Sie dem Beschuss"
1040 PRINT TAB(10)"von anderen Klingonen im Quadranten ausgesetzt."
1050 PRINT TAB(10)"Der Speicher-Computer ('COM'-Befehl) hat die"
1060 PRINT TAB(10)"M|glichkeit, die Torpedo-Flugbahn f}r Sie"
1070 PRINT TAB(10)"zu berechnen  (Wahl 2)":GOSUB 6820:PRINT CLS$
1100 PRINT EIN$;"SHE";AUS$;" BEFEHL = SCHILD-KONTROLLE":PRINT:PRINT
1120 PRINT TAB(10)"Bestimmt die Anzahl der Energieeinheiten, die zur"
1130 PRINT TAB(10)"Schild-Sicherung verwendet werden sollen, Energie"
1140 PRINT TAB(10)"wird von der Gesamtenergie des Schiffes genommen"
1150 PRINT TAB(10)"Beachten Sie, da~ die Gesamtenergie im Lagebericht"
1160 PRINT TAB(10)"die Schildenergie enth{lt.":GOSUB 6820:PRINT CLS$
1190 PRINT EIN$;"DAM";AUS$;" BEFEHL = ZERST\RUNGSKONTROLLBERICHT":PRINT:PRINT
1210 PRINT TAB(10)"Berichtet den Reparaturzustand aller Ger{te. Ein"
1220 PRINT TAB(10)"negativer 'REPARATURZUSTAND' bedeutet, das das"
1230 PRINT TAB(10)"Ger{t momentan zerst|rt ist.":GOSUB 6820:GOSUB 6850
1260 PRINT TAB(10)"Der Speicher-Computer gibt sechs Wahlm|glichkeiten":PRINT
1280 PRINT TAB(10)"WAHL 0 = ZUSAMMENFASSENDER WELTRAUMBERICHT":PRINT
1300 PRINT TAB(18)"Diese Wahl zeigt die Ergebnisse aller"
1310 PRINT TAB(18)"gespeicherten Kurz-und Fernbereichsberichte"
1320 PRINT TAB(18)"Eine Ausgabe auf den Drucker ist m|glich":PRINT 
1340 PRINT TAB(10)"WAHL 1 = LAGEBERICHT":PRINT
1360 PRINT TAB(18)"Diese Wahl zeigt die Anzahl der Klingonen,"
1370 PRINT TAB(18)"Sternendaten und im Spiel verbliebenen"
1380 PRINT TAB(18)"Sternenstationen.":PRINT
1400 PRINT TAB(10)"WAHL 2 = DATEN F]R PHOTONEN-TORPEDO":PRINT
1420 PRINT TAB(18)"Dies gibt die Richtung und Entfernung von der"
1430 PRINT TAB(18)"ENTERPRISE zu allen im Quadranten"
1440 PRINT TAB(18)"befindlichen Klingonen.":PRINT:GOSUB 6820:GOSUB 6850
1480 PRINT TAB(10)"WAHL 3 = STERNENSTATIONS-NAV-DATEN":PRINT
1500 PRINT TAB(18)"Diese Wahl gibt Richtung und Entfernung"
1510 PRINT TAB(18)"zu jeder im Quadranten befindlichen"
1520 PRINT TAB(18)"Sternenstation.":PRINT
1540 PRINT TAB(10)"WAHL 4 = RICHTUNGS-UND ENTFERNUNGSRECHNER":PRINT
1560 PRINT TAB(18)"Diese Wahl erm|glicht Eingaben zur"
1570 PRINT TAB(18)"Richtungs-und Entfernungskalkulation.":PRINT
1590 PRINT TAB(18)"WAHL 5 = KARTE DER WELTRAUM/GEBIETS-NAMEN":PRINT
1610 PRINT TAB(18)"Diese Wahl gibt die Namen der sechzehn"
1620 PRINT TAB(18)"gro~en Weltraumgebiete, die in diesem"
1630 PRINT TAB(18)"Spiel verwender werden, aus.":GOSUB 6820
2060 Z$="                         "
2070 A=INT(RND(1)*30000):RANDOMIZE A
2080 T=(INT(RND(1)*20)+20)*100:T0=T:T9=(INT(RND(1)*10)+25):D0=0:E=3000:E0=E
2090 P=10:P0=P:S9=200:S=0:B9=0:K9=0:X$="":X0$=" ist "
2100 '
2110 'LEGE AUSGANGSLAGE DER ENTERPRISE FEST
2120 '
2130 Q1=INT(RND(1)*8)+1:Q2=INT(RND(1)*8)+1:S1=INT(RND(1)*8)+1:S2=INT(RND(1)*8)+1
2140 FOR I=1 TO 9:C(I,1)=0:C(I,2)=0:NEXT
2150 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
2160 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
2170 FOR I=1 TO 8:D(I)=0:NEXT
2180 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
2190 '
2200 ' AUFSTELLEN,WAS SICH IM WELTRAUM BEFINDEN WIRD
2210 ' K3=ANZAHL DER KLINGONEN,B3=ANZAHL DER STATIONEN,S3=ANZAHL DER STERNE
2220 '
2230 FOR I=1 TO 8:FOR J=1 TO 8:K3=0:Z(I,J)=0:R1=RND(1)
2240 IF R1>.98 THEN K3=3:K9=K9+3:GOTO 2270
2250 IF R1>.95 THEN K3=2:K9=K9+2:GOTO 2270
2260 IF R1>.8 THEN K3=1:K9=K9+1
2270 B3=0:IF RND(1)>.96 THEN B3=1:B9=B9+1
2280 G(I,J)=K3*100+B3*10+(INT(RND(1)*8)+1):NEXT J,I:IF K9>T9 THEN T9=K9+1
2290 IF B9<>0 THEN 2320
2300 IF G(Q1,Q2)<200 THEN G(Q1,Q2)=G(Q1,Q2)+100:K9=K9+1
2310 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=INT(RND(1)*8)+1:Q2=INT(RND(1)*8)+1
2320 K7=K9:IF B9<>1 THEN X$="S":X0$=" sind "
2330 PRINT"Sie haben die folgenden Anweisungen:"
2340 PRINT"    Zerst|ren Sie die";K9;"Klingonen Kampfschiffe, die den Welt-"
2350 PRINT"    raum aufr}hren, bevor sie das Hauptquartier der"
2360 PRINT"    F|deration am Sternentag";T0+T9;"angreifen k|nnen."
2370 PRINT"    Es bleiben Ihnen";T9;"Tage."
2380 PRINT"    Es";X0$;B9;"Stationen im Raum,"
2390 PRINT"    an denen Sie das Schiff aufladen k|nnen. Wenn"
2400 PRINT"    Sie bereit sind, dr}cken Sie eine Taste."
2410 I$=INKEY$:IF I$="" THEN 2410
2420 '
2430 'IN NEUEN QUADRANT EINGEDRUNGEN
2440 '
2450 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=(INT(RND(1)*5)+1)/10:Z(Q1,Q2)=G(Q1,Q2)
2460 IF Q1<1 OR Q1>8 OR Q2<1 OR Q2>8 THEN 2560
2470 GOSUB 6360:PRINT:IF T0<>T THEN 2500
2480 PRINT"ZU BEGINN DER MISSION BEFINDET SICH DAS RAUMSCHIFF IM"
2490 PRINT"QUADRANTEN ";G2$;".":GOTO 2510
2500 PRINT"EINTRITT IN QUADRANTEN ";G2$". . ."
2510 PRINT:K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3
2520 S3=G(Q1,Q2)-100*K3-10*B3:IF K3=0 THEN 2550
2530 PRINT"KAMPFBEREICH    ZUSTAND ROT":IF S>200 THEN 2550
2540 PRINT"   ABWEHRSCHILDE GEFAEHRLICH NIEDRIG"
2550 FOR I=1 TO 3:K(I,1)=0:K(I,2)=0:NEXT
2560 FOR I=1 TO 3:K(I,3)=0:NEXT
2570 Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
2580 '
2590 'POSITIONIERE ENTERPRISE IM QUADRANTEN, DANACH K3 B3 S3
2600 '
2610 A$="<*>":Z1=S1:Z2=S2:GOSUB 6090:IF K3<1 THEN 2640
2620 FOR I=1 TO K3:GOSUB 6040:A$="+K+":Z1=R1:Z2=R2
2630 GOSUB 6090:K(I,1)=R1:K(I,2)=R2:K(I,3)=S9*(.5+RND(1)):NEXT
2640 IF B3<1 THEN 2660
2650 GOSUB 6040:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB 6090
2660 FOR I=1 TO S3:GOSUB 6040:A$=" * ":Z1=R1:Z2=R2:GOSUB 6090:NEXT
2670 GOSUB 4960
2680 IF S+E>10 THEN IF E>10 OR D(7)=0 THEN 2720
2690 PRINT:PRINT"**Ernsthafter Fehler ** Sie sind gerade mit dem Schiff"
2700 PRINT"im Raum gestrandet":PRINT"Sie haben ungen}gende Man|vrierenergie":PRINT"und die Schild-Kontrolle ist gegenw{rtig unf{hig,"
2710 PRINT"zum Maschinenraum umzuschalten!!!":GOTO 4780
2720 INPUT"BEFEHL ";A$
2730 FOR I=1 TO 9:IF LEFT$(A$,3)<>MID$(A1$,3*I-2,3) THEN 2750
2740 ON I GOTO 2880,2670,3680,3790,4010,4350,4460,5220,4810
2750 NEXT:PRINT"TIPPE EINEN DER FOLGENDEN BEFEHLE:"
2760 PRINT"  NAV (FUER RICHTUNGSAENDERUNG)"
2770 PRINT"  SRS (FUER NAHBEREICHSUEBERSICHT)"
2780 PRINT"  LRS (FUER FERNBEREICHS-UEBERSICHT)"
2790 PRINT"  PHA (UM PHASER ABZUFEUERN)
2800 PRINT"  TOR (UM PHOTONEN-TORPEDOS ABZUFEUERN)"
2810 PRINT"  SHE (UM SCHILDE ZU HEBEN ODER ZU SENKEN)"
2820 PRINT"  DAM (FUER ZERSTOERUNGSBERICHT)"
2830 PRINT"  COM (UM DEN SPEICHER COMPUTER ZU BEFRAGEN)"
2840 PRINT"  XXX ( UM BEFEHL ABZUBRECHEN)":GOTO 2680
2850 '
2860 'KURSKONTROLLE BEGINNT HIER
2870 '
2880 INPUT"KURS (1-9)";C1:IF C1=9 THEN C1=1
2890 IF C1>=1 AND C1<9 THEN 2910
2900 PRINT"     LT. SULU BERICHTET: 'FALSCHE KURSDATEN'":GOTO 2680
2910 X$="8":IF D(1)<0 THEN X$="0.2"
2920 PRINT"FAKTOR (0 - ";X$;")";:INPUT W1:IF D(1)<0 AND W1>.2 THEN 2970
2930 IF W1>0 AND W1<=8 THEN 2980
2940 IF W1=0 THEN 2680
2950 PRINT"     CHEF-INGENIEUR KOCH BERICHTET: 'DIE MASCHINEN VERKRAFTEN"
2960 PRINT"     FAKTOR";W1;"NICHT !'":GOTO 2680
2970 PRINT"     RICHTUNGSMOTOREN SIND ZERSTOERT. HOECHSTGESCHW.= 0.2":GOTO 2680
2980 N=INT(W1*8+.5):IF E-N>=0 THEN 3080
2990 PRINT"MASCHINISTEN BERICHTEN:  'UNGENUEGEND ENERGIE, UM MIT"
3000 PRINT"             GESCHW.";W1;"ZU MANOEVRIEREN."
3010 IF S<N-E OR D(7)<0 THEN 2680
3020 PRINT"ABSCHIRM-KONTROLLRAUM BESTAETIGT: ";S;"ENERGIEEINHEITEN"
3030 PRINT"                                  FUER SCHILDE VERWENDET"
3040 GOTO 2680
3050 '
3060 ' KLINGONEN ZIEHEN/SCHIESSEN GEGEN ENTERPRISE
3070 '
3080 FOR I=1 TO K3:IF K(I,3)=0 THEN 3110
3090 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB 6090:GOSUB 6040
3100 K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB 6090
3110 NEXT:GOSUB 4630:D1=0:D6=W1:IF W1>=1 THEN D6=1
3120 FOR I=1 TO 8:IF D(I)>=0 THEN 3170
3130 D(I)=D(I)+D6:IF D(I)>-.1 AND D(I)<0 THEN D(I)=-.1:GOTO 3170
3140 IF D(I)<0 THEN 3170
3150 IF D1<>1 THEN D1=1:PRINT"Zerst|rungsbericht :  ";
3160 PRINT TAB(8);:R1=I:GOSUB 6170:PRINT G2$;" REPARATUR FERTIG."
3170 NEXT:IF RND(1) >.2 THEN 3240
3180 R1=INT(RND(1)*8)+1:IF RND(1)>=.6 THEN 3210
3190 D(R1)=D(R1)-(INT(RND(1)*5)+1):PRINT"Zerst|rungsbericht :   ";
3200 GOSUB 6170:PRINT G2$;" zerst|rt":PRINT:GOTO 3240
3210 D(R1)=D(R1)+(INT(RND(1)*3)+1):PRINT"Zerst|rungsbericht :   ";
3220 GOSUB 6170:PRINT G2$;" REPARATURZUSTAND VERBESSERT":PRINT
3230 'ENTERPRISE BEGINNT SICH ZU BEWEGEN
3240 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB 6090
3250 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
3260 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
3270 FOR I=1 TO N:S1=S1+X1:S2=S2+X2:IF S1<1 OR S1>=9 OR S2<1 OR S2>=9 THEN 3420
3280 S8=INT(S1)*24+INT(S2)*3-26:IF MID$(Q$,S8,2)="  " THEN 3310
3290 S1=INT(S1-X1):S2=INT(S2-X2):PRINT"LENKMOTOREN AUSGESCHALTET IN";
3300 PRINT" SEKTOR";S1;",";"WEGEN SCHLECHTER":PRINT"NAVIGATION":GOTO 3320
3310 NEXT:S1=INT(S1):S2=INT(S2)
3320 A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB 6090:GOSUB 3610:T8=1
3330 IF W1<1 THEN T8=.1*INT(10*W1)
3340 T=T+T8:IF T>T0+T9 THEN 4780
3350 '
3360 ' UEBERPRUEFE OB GESTRANDET, DANN FRAGE NACH BEFEHL
3370 '
3380 GOTO 2670
3390 '
3400 ' QUADRANTEN-GRENZEN UEBERSCHRITTEN
3410 '
3420 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
3430 S2=INT(Y-Q2*8):IF S1=0 THEN Q1=Q1-1:S1=8
3440 IF S2=0 THEN Q2=Q2-1:S2=8
3450 X5=0:IF Q1<1 THEN X5=1:Q1=1:S1=1
3460 IF Q1>8 THEN X5=1:Q1=8:S1=8
3470 IF Q2<1 THEN X5=1:Q2=1:S2=1
3480 IF Q2>8 THEN X5=1:Q2=8:S2=8
3490 IF X5=0 THEN 3560
3500 PRINT"LT. UHURA BERICHTET MELDUNG VOM FLOTTEN-KOMMANDO:"
3510 PRINT"  'ERLAUBNIS ZUM VERSUCH DIE AEUSSEREN GRENZEN DES WELTALLS":PRINT"  ZU DURCHBRECHEN, WIRD HIERMIT"
3520 PRINT"          *** VERWEIGERT ***. SCHALTE MOTOREN AUS"
3530 PRINT"CHEF-INGENIEUR KOCH BERICHTET: 'LENKMOTOREN AUSGESCHALTET"
3540 PRINT"  AUF SEKTOR";S1;",";S2;"DES QUADRANTEN";Q1;",";Q2;" ."
3550 IF T>T0+T9 THEN 4780
3560 IF 8*Q1+Q2=8*Q4+Q5 THEN 3320
3570 T=T+1:GOSUB 3610:GOTO 2450
3580 '
3590 ' MANOEVRIERENERGIE
3600 '
3610 E=E-N-10:IF E>=0 THEN RETURN
3620 PRINT"SCHILD KONTROLLE GIBT ENERGIE AB, UM MANOEVER ABZUSCHLIESSEN"
3630 S=S+E:E=0:IF S<=0 THEN S=0
3640 RETURN
3650 '
3660 ' FERNBEREICHSUEBERSICHT
3670 '
3680 IF D(3)<0 THEN PRINT"FERNBEREICHSSENSOREN SIND NICHT BETRIEBSFAEHIG": GOTO 2680
3690 PRINT"FERNBEREICHS-UEBERSICHT FUER QUADRANT";Q1;",";Q2
3700 O1$="------------------":PRINT O1$
3710 FOR I=Q1-1 TO Q1+1:N(1)=-1:N(2)=-2:N(3)=-3:FOR J=Q2-1 TO Q2+1
3720 IF I>0 AND I<9 AND J>0 AND J<9 THEN N(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
3730 NEXT J:FOR L=1 TO 3:PRINT": ";:IF N(L)<0 THEN PRINT"*** ";:GOTO 3750
3740 PRINT RIGHT$(STR$(N(L)+1000),3);" ";
3750 NEXT L:PRINT":":PRINT O1$:NEXT I:GOTO 2680
3760 '
3770 'PHASER KONTROLLE HIER
3780 '
3790 IF D(4)<0 THEN PRINT"PHASER NICHT BETRIEBSFAEHIG": GOTO 2680
3800 IF K3>0 THEN 3830
3810 PRINT"TECHNISCHER OFFIZIER SPOCK BErICHTET: 'FUEHLER ZEIGEN KEINE"
3820 PRINT"               FEINDSCHIFFE IM QUADRANTEN'":GOTO 2680
3830 IF D(8)<0 THEN PRINT"COMPUTER-FEHLER VERFAELSCHT GENAUIGKEIT"
3840 PRINT"PHASER AUF ZIEL EINGESTELLT;   ";
3850 PRINT"VERFUEGBARE ENERGIE = ";E;"EINHEITEN"
3860 INPUT"WIEVIELE EINHEITEN ABSCHIESSEN";X:IF X<=0 THEN 2680
3870 IF E-X<0 THEN 3850
3880 E=E-X:IF D(7)<0 THEN X=RND(1)*X
3890 H1=INT(X/K3):FOR I=1 TO 3:IF K(I,3)<=0 THEN 3970
3900 H=INT((H1/SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2))*(RND(1)+2)):IF H>.15*K(I,3) THEN 3920
3910 PRINT"FUEHLER BERICHTEN KEINE BESCHAEDIGUNG DES FEINDES AUF";K(I,1);",";K(I,2):GOTO 3970
3920 K(I,3)=K(I,3)-H:PRINT"KLINGON AUF SEKTOR";K(I,1);",";K(I,2);"MIT";H;"EIN-"
3930 PRINT"HEITEN GETROFFEN":IF K(I,3)<=0 THEN PRINT"*** KLINGON ZERSTOERT ***":GOTO 3950
3940 PRINT"   (FUEHLER ZEIGEN: NOCH";K(I,3);"EINHEITEN UEBRIG)":GOTO 3970
3950 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB 6090
3960 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IF K9<=0 THEN 4900
3970 NEXT I:GOSUB 4630:GOTO 2680
3980 '
3990 'PHOTONEN-TORPEDO
4000 '
4010 IF P<=0 THEN PRINT"ALLE PHOTONEN-TORPEDOS VERBRAUCHT":GOTO 2680
4020 IF D(5)<0 THEN PRINT"PHOTONEN-ROEHREN SIND AUSSER BETRIEB":GOTO 2680
4030 INPUT"KURS DES PHOTONEN-TORPEDOS (1-9)";C1:IF C1=9 THEN C1=1
4040 IF C1>=1 AND C1<9 THEN 4070
4050 PRINT"FAEHNRICH CHEKOV BERICHTET: 'FALSCHE KURS-DATEN, SIR!'"
4060 GOTO 2680
4070 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
4080 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
4090 PRINT"TORPEDO-BAHN:"
4100 X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
4110 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN 4310
4120 PRINT"               ";X3;",";Y3:A$="   ":Z1=X:Z2=Y:GOSUB 6290
4130 IF Z3<>0 THEN 4100
4140 A$="+K+":Z1=X:Z2=Y:GOSUB 6290:IF Z3=0 THEN 4190
4150 PRINT"*** KLINGON ZERSTOERT ***":K3=K3-1:K9=K9-1:IF K9<=0 THEN 4900
4160 FOR I=1 TO 3:IF X3=K(I,1) AND Y3=K(I,2) THEN 4180
4170 NEXT I:I=3
4180 K(I,3)=0:GOTO 4290
4190 A$=" * ":Z1=X:Z2=Y:GOSUB 6290:IF Z3=0 THEN 4210
4200 PRINT"STERN AUF";X3;",";Y3;"ABSORBIERTE TORPEDO-ENERGIE.":GOSUB 4630:GOTO 2680
4210 A$=">!<":Z1=X:Z2=Y:GOSUB 6290:IF Z3=0 THEN 4030
4220 PRINT"*** STERNENSTATION ZERSTOERT ***":B3=B3-1:B9=B9-1
4230 IF B9>0 OR K9>T-T0-T9 THEN 4270
4240 PRINT"SO GESCHIEHT ES, KAPITAEN!! DAMIT VERLIEREN SIE DAS KOMMANDO"
4250 PRINT"UND WERDEN ZU 99 STERNENTAGEN HARTER ARBEIT AUF CYGNUS STRAFVERSETZT !!"
4260 GOTO 4810
4270 PRINT"FLOTTENKOMMANDO UEBERPRUEFT IHRE DATEN FUER DAS":PRINT"MILITAERGERICHT !!"
4280 D0=0
4290 Z1=X:Z2=Y:A$="   ":GOSUB 6090
4300 G(Q1,Q2)= K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB 4630:GOTO 2680
4310 PRINT"TORPEDO VERFEHLTE":GOSUB 4630:GOTO 2680
4320 '
4330 ' SCHILD-KONTROLLE
4340 '
4350 IF D(7)<0 THEN PRINT"SCHILD-KONTROLLE AUSSER BETRIEB":GOTO 2680
4360 PRINT"VERFUEGBARE ENERGIE =";E+S;:INPUT"WIEVIEL EINHEITEN FUER SCHILDE";X
4370 IF X=<0 OR S=X THEN PRINT"(SCHILDE UNVERAENDERT)":GOTO 2680
4380 IF X<=E+S THEN 4410
4390 PRINT"SCHILD-KONTROLLE BERICHTET: 'DAS IST NICHT DAS FINANZAMT":PRINT"                             DER FOEDERATION"
4400 PRINT"(SCHILDE UNVERAENDERT)":GOTO 2680
4410 E=E+S-X:S=X:PRINT"ABSCHIRM-KONTROLLRAUM BERICHTET:"
4420 PRINT"   'SCHILDE BENOETIGEN JETZT";INT(S);"EINHEITEN PRO BEFEHL.'":GOTO 2680
4430 '
4440 ' ZERSTOERUNGSUEBERWACHUNG
4450 '
4460 IF D(6)>=0 THEN 4560
4470 PRINT"ZERSTOERUNGS-UEBERSICHT NICHT VERFUEGBAR":IF D=0 THEN 2680
4480 D3=0:FOR I=1 TO 8:IF D(I)<0 THEN D3=D3+.1
4490 NEXT:IF D3=0 THEN 2680
4500 PRINT:D3=D3+D4:IF D3>=1 THEN D3=.9
4510 PRINT"TECHNIKER SIND FUER REPARATUREN DES RAUMSCHIFFES ABRUFBEREIT"
4520 PRINT"GESCHAETZTE REPARATURDAUER:";.01*INT(100*D3);"STERNENTAGE"
4530 PRINT"GENEHMIGEN SIE DIE REPARATUR ";:GOSUB 6790:IF K$<>"J" THEN 2680
4540 FOR I=1 TO 8:IF D(I)<0 THEN D(I)=0
4550 NEXT:T=T+D3+.1
4560 PRINT:PRINT"GERAET                    REPARATURZUSTAND":FOR R1=1 TO 8
4570 GOSUB 6170:PRINT G2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
4580 NEXT:PRINT:IF D0<>0 THEN 4480
4590 GOTO 2680
4600 '
4610 'KLINGONEN SCHIESSEN
4620 '
4630 IF K3<=0 THEN RETURN
4640 IF D0<>0 THEN PRINT"SCHILDE DER STATION SCHUETZEN DIE ENTERPRISE":RETURN
4650 FOR I=1 TO 3:IF K(I,3)<=0 THEN 4740
4660 H=INT((K(I,3)/SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2))*(2+RND(1))):S=S-H:K(I,3)=K(I,3)/(3+RND(1))
4670 PRINT"EINHEIT";H;"WURDE VOM SEKTOR";K(I,1);",";K(I,2);"GETROFFEN"
4680 IF S<=0 THEN 4790
4690 PRINT"      (SCHILDE AUF";S;"EINHEITEN VERMINDERT)":IF H<20 THEN 4740
4700 IF RND(1)>.6 OR H/S<=.02 THEN 4740
4710 R1=INT(RND(1)*8)+1:D(R1)=D(R1)-H/S-.5*RND(1):GOSUB 6170
4720 PRINT"ZERSTOERUNGS-KONTROLLE BERICHTET: '";G2$;" DURCH TREFFER BESCHAEDIGT'"
4730 NEXT
4740 RETURN
4750 '
4760 ' Enterprise zerst|rt
4770 '
4780 PRINT"AM STERNENTAG";T:GOTO 4810
4790 PRINT:PRINT"DIE ENTERPRISE WURDE ZERSTOERT. DIE FOEDERATION"
4800 PRINT"WIRD EROBERT":GOTO 4780
4810 PRINT"AM ENDE DER MISSION SIND NOCH";K9;"SCHLACHTSCHIFFE DER"
4820 PRINT"KLINGONEN UEBRIG."
4830 PRINT:PRINT:IF B9=0 THEN 4890
4840 PRINT"DIE FOEDERATION BENOETIGT FUER EINE AEHNLICHE AUFGABE EINEN"
4850 PRINT"RAUMSCHIFF-KOMMANDANTEN. - WENN EIN FREIWILLIGER ANWESEND"
4860 PRINT"IST, SOLL ER VORTRETEN UND SICH MELDEN  ";:GOSUB 6790:IF K$="J" THEN 2070
4870 PRINT"***** SCHADE ***** ES GIBT KEINE GUTEN KOMMANDANTEN MEHR"
4880 PRINT TAB(40)"* TSCHUESS *":GOSUB 6820:PRINT CLS$:END
4890 GOTO 4840
4900 PRINT"GRATULIERE KAPITAEN ! DAS LETZTE SCHLACHTSCHIFF DER KLINGONEN,"
4910 PRINT"DAS DIE FOEDERATION BEDROHTE, WURDE ZERSTOERT.":PRINT
4920 PRINT"DEINE ERFOLGSBEWERTUNG IST:";1000*(K7/(T-T0))^2:GOTO 4830
4930 '
4940 ' KURZBEREICHS-ABSUCHGERAETE UND BEGINN UNTERPROGRAMM
4950 '
4960 FOR I=S1-1 TO S1+1:FOR J=S2-1 TO S2+1
4970 IF INT(I+.5)<1 OR INT(I+.5)>8 OR INT(J+.5)<1 OR INT(J+.5)>8 THEN 4990
4980 A$=">!<":Z1=I:Z2=J:GOSUB 6290:IF Z3=1 THEN 5000
4990 NEXT J,I:D0=0:GOTO 5020
5000 D0=1:C$="ANGELEGT":E=E0:P=P0
5010 PRINT"SCHILDE FUER ANLEGEMANOEVER GESENKT":S=0:GOTO 5040
5020 IF K3>0 THEN C$="*ROT*":GOTO 5040
5030 C$="GRUEN":IF E<E0*.1 THEN C$="GELB"
5040 IF D(2)>=0 THEN 5060
5050 PRINT:PRINT"*** KURZBEREICHSFUEHLER AUSGEFALLEN ***":PRINT:RETURN
5060 O1$="----------------------------------":PRINT O1$
5070 FOR I=1 TO 8
5080 FOR J=((I-1)*24+1) TO ((I-1)*24+22) STEP 3:PRINT" ";MID$(Q$,J,3);:NEXT J
5090 ON I GOTO 5100,5110,5120,5130,5140,5150,5160,5170
5100 PRINT TAB(38)"STERNENTAG        ";INT(T*10)*.1:GOTO 5180
5110 PRINT TAB(38)"ZUSTAND           ";C$:GOTO 5180
5120 PRINT TAB(38)"QUADRANT          ";Q1;",";Q2:GOTO 5180
5130 PRINT TAB(38)"SEKTOR            ";S1;",";S2:GOTO 5180
5140 PRINT TAB(38)"PHOTONEN-TORPEDOS";INT(P):GOTO 5180
5150 PRINT TAB(38)"GESAMT-ENERGIE   ";INT(E+S):GOTO 5180
5160 PRINT TAB(38)"SCHILDE          ";INT(S):GOTO 5180
5170 PRINT TAB(38)"KLINGONEN UEBRIG";INT(K9)
5180 NEXT I:PRINT O1$:RETURN
5190 '
5200 ' SPEICHER-COMPUTER-CODE
5210 '
5220 IF D(8)<0 THEN PRINT"COMPUTER AUSSER BETRIEB":GOTO 2680
5230 INPUT"COMPUTER AKTIV UND WARTET AUF BEFEHL";A:IF A<0 THEN 2680
5240 PRINT:H8=1:ON A+1 GOTO 5390,5570,5680,5980,5740,5350
5250 PRINT"VERFUEGBARE FUNKTIONEN DES SPEICHER-COMPUTERS:"
5260 PRINT"   0 = ZUSAMMENFASSENDE WELTRAUM-UEBERSICHT"
5270 PRINT"   1 = ZUSTANDS-BERICHT"
5280 PRINT"   2 = PHOTONEN-TORPEDO-DATEN"
5290 PRINT"   3 = STERNENSTATIONS-NAV-DATEN"
5300 PRINT"   4 = RICHTUNGS/ENTFERNUNGS-RECHNER"
5310 PRINT"   5 = 'BEREICHS-NAMEN' - STERNENKARTE":PRINT:GOTO 5230
5320 '
5330 ' AUFSTELLUNG DER STERNENKARTE
5340 '
5350 H8=0:G5=1:PRINT"                        Weltraum":GOTO 5440
5360 '
5370 ' ZUSAMMENFASSENDER WELTRAUM-BERICHT
5380 '
5390 PRINT"M|chten Sie einen Ausdruck ? Ist der Drucker eingeschaltet ?";:GOSUB 6790
5400 IF K$="J" OR K$="j" THEN GOSUB 6650:GOTO 2680
5410 PRINT:PRINT"        ";
5420 PRINT"Computer-Weltraumbericht f}r Quadrant ";Q1;",";Q2
5430 PRINT
5440 PRINT"        1      2      3      4      5      6      7      8"
5450 O1$="      -----  -----  -----  -----  -----  -----  -----  -----"
5460 PRINT O1$
5470 FOR I=1 TO 8:PRINT I;:IF H8=0 THEN 5510
5480 FOR J=1 TO 8:PRINT"    ";:IF Z(I,J)=0 THEN PRINT"***";:GOTO 5500
5490 PRINT RIGHT$(STR$(Z(I,J)+1000),3);
5500 NEXT J:GOTO 5530
5510 Z4=I:Z5=1:GOSUB 6360:J0=INT(15-.5*LEN(G2$)):PRINT TAB(J0);G2$;
5520 Z5=5:GOSUB 6360:J0=INT(39-.5*LEN(G2$)):PRINT TAB(J0);G2$;
5530 PRINT:PRINT O1$:NEXT I:GOTO 2680
5540 '
5550 ' ZUSTANDSBERICHT
5560 '
5570 PRINT"   Zustands-Bericht:":X$="":IF K9>1 THEN X$="en"
5580 PRINT"Klingon";X$" }brig:";K9
5590 PRINT"Auftrag mu~ in";.1*INT(T0+T9-T)*10;"Sternentagen beendet sein"
5600 X$="en":IF B9<2 THEN X$="":IF B9<1 THEN 5630
5610 PRINT"Die F|deration unterh{lt ";B9;" Sternenstation";X$;" im Weltraum"
5620 GOTO 4460
5630 PRINT"Wegen Deiner Dummheit verbleibst Du allein im"
5640 PRINT"  Weltraum -- es ist keine Sternenstation }brig !":GOTO 4460
5650 '
5660 ' TORPEDO,STATIONS-NAV,R/E-RECHNER
5670 '
5680 IF K3<=0 THEN 3810
5690 X$="":IF K3>1 THEN X$="EN"
5700 PRINT"VON DER ENTERPRISE ZU KLINGONEN-SCHLACHTSCHIFF";X$
5710 H8=0:FOR I=1 TO 3:IF K(I,3)<=0 THEN 5940
5720 W1=K(I,1):X=K(I,2)
5730 C1=S1:A=S2:GOTO 5780
5740 PRINT"RICHTUNGS/ENTFERNUNGS-RECHNER:"
5750 PRINT"SIE SIND IM QUADRANTEN ";Q1;",";Q2;"  SEKTOR ";S1;",";S2
5760 PRINT"BITTE ANFANGSKOORDINATEN EINGEBEN (X,Y)";:INPUT C1,A
5770 PRINT"BITTE ENDKOORDINATEN EINGEBEN (X,Y)";:INPUT W1,X
5780 X=X-A:A=C1-W1
5790 IF X<0 THEN 5870
5800 IF A<0 THEN 5890
5810 IF X>0 THEN 5830
5820 IF A=0 THEN C1=5:GOTO 5840
5830 C1=1
5840 IF ABS(A)<=ABS(X) THEN 5860
5850 PRINT"RICHTUNG =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO 5930
5860 PRINT"RICHTUNG =";C1+(ABS(A)/ABS(X)):GOTO 5930
5870 IF A>0 THEN C1=3:GOTO 5900
5880 IF X<>0 THEN C1=5:GOTO 5840
5890 C1=7
5900 IF ABS(A)>=ABS(X) THEN 5920
5910 PRINT"RICHTUNG =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO 5930
5920 PRINT"RICHTUNG =";C1+(ABS(X)/ABS(A))
5930 PRINT"ENTFERNUNG =";SQR(X^2+A^2):IF H8=1 THEN 2680
5940 NEXT I:GOTO 2680
5950 '
5960 ' Kurs zur Sternenstation
5970 '
5980 IF B3<>0 THEN PRINT"VON DER ENTERPRISE ZUR STERNENSTATION:":W1=B4:X=B5:GOTO 5730
5990 PRINT"MSTR. SPOCK BERICHTET:  'FUEHLER ZEIGT KEINE STERNENSTATION"
6000 PRINT"                         IN DIESEM QUADRANTEN'":GOTO 2680
6010 '
6020 ' FINDE LEEREN RAUM IM QUADRANTEN(FUER DINGE)
6030 '
6040 R1=INT((RND(1)*8)+1):R2=INT((RND(1)*8)+1):A$="   ":Z1=R1:Z2=R2:GOSUB 6290:IF Z3=0 THEN 6050
6050 RETURN
6060 '
6070 ' GIB QUADRANTEN-DATEN IN FELD EIN
6080 '
6090 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
6100 IF LEN(A$)<>3 THEN PRINT"FEHLER":STOP
6110 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
6120 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
6130 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN
6140 '
6150 ' DRUCKE GERAETE-NAMEN
6160 '
6170 ON R1 GOTO 6180,6190,6200,6210,6220,6230,6240,6250
6180 G2$="RICHTUNGS-MOTOREN":RETURN
6190 G2$="NAHBEREICHS-FUEHLER":RETURN
6200 G2$="FERNBEREICHS-FUEHLER":RETURN
6210 G2$="PHASER-KONTROLLE":RETURN
6220 G2$="PHOTONEN-ROEHREN":RETURN
6230 G2$="ZERSTOERUNGS-KONTROLLE":RETURN
6240 G2$="SCHILD-KONTROLLE":RETURN
6250 G2$="SPEICHER-COMPUTER":RETURN
6260 '
6270 ' ZEICHENFOLGEEN-VERGLEICH IM QUADRANTENFELD
6280 '
6290 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
6300 IF MID$(Q$,S8,3)<>A$ THEN RETURN
6310 Z3=1:RETURN
6320 '
6330 ' QUADRANTEN-NAMEN IN G2$ VON Z4,Z5(=Q1,Q2)
6340 ' MIT G5=1 UM NUR GEBIETSNAMEN ZU ERHALTEN
6350 '
6360 IF Z5<=4 THEN ON Z4 GOTO 6380,6390,6400,6410,6420,6430,6440,6450
6370 GOTO 6460
6380 G2$="ANTARES":GOTO 6550
6390 G2$="RIGEL":GOTO 6550
6400 G2$="PROCYON":GOTO 6550
6410 G2$="VEGA":GOTO 6550
6420 G2$="CANOPUS":GOTO 6550
6430 G2$="ALTAIR":GOTO 6550
6440 G2$="SAGITTARIUS":GOTO 6550
6450 G2$="POLLUX":GOTO 6550
6460 ON Z4 GOTO 6470,6480,6490,6500,6510,6520,6530,6540
6470 G2$="SIRIUS":GOTO 6550
6480 G2$="DENEB:GOTO 9210
6490 G2$="CAPELLA":GOTO 6550
6500 G2$="BETEIGEUZE":GOTO 6550
6510 G2$="ALDEBARAN":GOTO 6550
6520 G2$="REGULUS":GOTO 6550
6530 G2$="ARTURUS":GOTO 6550
6540 G2$="SPICA"
6550 IF G5<>1 THEN ON Z5 GOTO 6570,6580,6590,6600,6570,6580,6590,6600
6560 RETURN
6570 G2$=G2$+" I":RETURN
6580 G2$=G2$+" II":RETURN
6590 G2$=G2$+" III":RETURN
6600 G2$=G2$+" IV":RETURN
6610 END
6620 '
6630 ' Druckroutine
6640 '
6650 LPRINT:LPRINT"        ";
6660 LPRINT"Computer-Weltraumbericht f}r Quadrant ";Q1;",";Q2
6670 LPRINT
6680 LPRINT"        1      2      3      4      5      6      7      8"
6690 O1$="      -----  -----  -----  -----  -----  -----  -----  -----"
6700 LPRINT O1$
6710 FOR I=1 TO 8:LPRINT I;:IF H8=0 THEN 6750
6720 FOR J=1 TO 8:LPRINT"    ";:IF Z(I,J)=0 THEN LPRINT"***";:GOTO 6740
6730 LPRINT RIGHT$(STR$(Z(I,J)+1000),3);
6740 NEXT J:GOTO 6770
6750 Z4=I:Z5=1:GOSUB 6360:J0=INT(15-.5*LEN(G2$)):LPRINT TAB(J0);G2$;
6760 Z5=5:GOSUB 6360:J0=INT(39-.5*LEN(G2$)):LPRINT TAB(J0);G2$;
6770 LPRINT:LPRINT O1$:NEXT I
6780 RETURN
6790 PRINT"J/N"
6800 K$=INKEY$:IF K$="" THEN 6800
6810 RETURN
6820 K$=INKEY$:IF K$="" THEN 6820
6830 IF K$<>"J" THEN K$="N"
6840 RETURN
6850 ' ]berschrift f}r Anleitung COM-Befehl
6855 '
6860 PRINT CLS$:PRINT EIN$;"COM";AUS$;" BEFEHL = SPEICHER-COMPUTER":PRINT:PRINT
6890 RETURN
6900 '
6910 ' Initialisierung
6920 '
6930 A$ ="**************************************"
6940 A4$="*                                    *"
6950 Z$="                          "
6960 A2$="*  * *  S U P E R  STAR TREK  * *    *"
6970 A3$="======================================"
6980 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
6990 CLS$=CHR$(26):WIDTH 255
7000 DEF FNCU$(X,Y)=CHR$(27)+CHR$(61)+CHR$(32+Y)+CHR$(32+X)
7010 EIN$=CHR$(27)+CHR$(82):AUS$=CHR$(27)+CHR$(83):CLS$=CHR$(26)
7020 HAND$=CHR$(&H99)+CHR$(&H9A)+CHR$(&H9B)
7030 RETURN
)
701