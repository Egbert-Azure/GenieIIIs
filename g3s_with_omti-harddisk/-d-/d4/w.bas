1 'Expertensystem / Inferenzkomponente
2 'Dr. H.-J. Soll      1987
3 'modifiziert f}r Tandy TRS80 Mod.1
4 'von Egbert Schr|er  1990
10 CLS
20 MSTACK%=100
30 DIM SRNR%(MSTACK%),PZAEHLER%(MSTACK%)
40 PRINT @581,"Name der Wissensbasis : ";:INPUT WN$
50 OPEN "I",1,WN$
60 INPUT #1,OBJEKTE%:INPUT #1,FRAGEN%:INPUT #1,REGELN%
70 INPUT #1,Z1%:INPUT #1,Z2%
80 DIM OA$(OBJEKTE%),OSTATUS%(OBJEKTE%),ODIAG%(OBJEKTE%)
90 DIM FTEXT$(FRAGEN%),FA$(2,FRAGEN%),FSTATUS%(FRAGEN%),FOBJEKT$(FRAGEN%)
100 DIM RSTATUS%(REGELN%),WENN$(5,REGELN%),WF%(5,REGELN%),DANN$(REGELN%),TYP%(REGELN%)
110 IF OBJEKTE%=0 GOTO 150
120 FOR N%=1 TO OBJEKTE%
130 INPUT#1,OA$(N%):INPUT#1,OSTATUS%(N%):INPUT#1,ODIAG%(N%)
140 NEXT N%
150 IF FRAGEN%=0 GOTO 190
160 FOR N%=1 TO FRAGEN%
170 INPUT #1,FTEXT$(N%):INPUT #1,FA$(1,N%):INPUT #1,FA$(2,N%):INPUT #1,FSTATUS%(N%):INPUT #1,FOBJEKT$(N%)
180 NEXT N%
190 IF REGELN%=0 GOTO 270
200 FOR N%=1 TO REGELN%
210 INPUT #1,RSTATUS%(N%)
220 FOR M%=1 TO 5
230 INPUT #1,WENN$(M%,N%):INPUT #1,WF%(M%,N%)
240 NEXT M%
250 INPUT #1,DANN$(N%):INPUT #1,TYP%(N%)
260 NEXT N%
270 CLOSE #1
280 N%=0
290 FOR M%=1 TO REGELN%
300 IF TYP%(M%)=1 THEN N%=N%+1
310 NEXT M%
320 CLS
330 PRINT @133,FRAGEN%;" Fragen"
340 PRINT @197,REGELN%;" Regeln"
350 PRINT @261,N%;" Diagnosen"
360 PRINT @657,"Bitte dr}cken Sie eine Taste"
370 I$=INKEY$:IF I$="" GOTO 370
1000 'Status zur}cksetzen
1010 FOR N%=1 TO OBJEKTE%
1020 OSTATUS%(N%)=0
1030 NEXT N%
1040 FOR N%=1 TO FRAGEN%
1050 FSTATUS%(N%)=0
1060 NEXT N%
1070 FOR N%=1 TO REGELN%
1080 RSTATUS%(N%)=0
1090 NEXT N%
1100 'alle m|glichen Diagnosen auf Stack ablegen
1110 STACK%=0
1120 FOR N%=1 TO REGELN%
1130 IF TYP%(N%)=1 GOSUB 3220:'Stack ablegen
1140 NEXT N%
2000 'eigentliches Infernzprogramm
2010 IF STACK%=0 GOTO 4010:'Diagnosen ausgeben
2020 IF RSTATUS%(SRNR%(STACK%))=0 GOTO 2510
2030 STACK%=STACK%-1
2040 GOTO 2010
2500 'Regeln bearbeiten
2510 IF (PZAEHLER%(STACK%)>5 AND WENN$(1,SRNR%(STACK%))<>"- oder -") THEN RSTATUS%(SRNR%(STACK%))=1:NM$=DANN$(SRNR%(STACK%)):GOSUB 3430:OSTATUS%(N%)=1:GOTO 2030
2520 IF (PZAEHLER%(STACK%)>5 AND WENN$(1,SRNR%(STACK%))="- oder -") THEN RSTATUS%(SRNR%(STACK%))=-1:NM$=DANN$(SRNR%(STACK%)):GOSUB 3430:OSTATUS%(N%)=-1:GOTO 2030
2530 NM$=WENN$(PZAEHLER%(STACK%),SRNR%(STACK%))
2540 IF NM$="" THEN PZAEHLER%(STACK%)=6:GOTO 2510
2550 IF NM$="- oder -" THEN PZAEHLER%(STACK%)=PZAEHLER%(STACK%)+1:GOTO 2510
2560 GOSUB 3430
2570 IF OSTATUS%(N%)=0 GOTO 2510
2580 IF WENN$(1,SRNR%(STACK%))="- oder -" GOTO 2640
2590 'und-Regel
2600 IF OSTATUS%(N%)*WF%(PZAEHLER%(STACK%),SRNR%(STACK%))<>1 THEN RSTATUS%(SRNR%(STACK%))=(-1):NM$=DANN$(SRNR%(STACK%)):GOSUB 3430:OSTATUS%(N%)=(-1):GOTO 2030
2610 PZAEHLER%(STACK%)=PZAEHLER%(STACK%)+1
2620 GOTO 2510
2630 'oder-Regel
2640 IF OSTATUS%(N%)*WF%(PZAEHLER%(STACK%),SRNR%(STACK%))=1 THEN RSTATUS%(SRNR%(STACK%))=1:NM$=DANN$(SRNR%(STACK%)):GOSUB 3430:OSTATUS%(N%)=1:GOTO 2030
2650 PZAEHLER%(STACK%)=PZAHLER%(STACK%)+1
2660 GOTO 2510
3000 'Objektstatus unbekannt
3010 '-> n%  : Nr des unbestimmten Objektes
3020 FOR K%=1 TO REGELN%
3030 IF DANN$(K%)=OA$(N%) THEN M%=N%:N%=K%:GOSUB 3220:N%=M%:GOTO 2020
3040 NEXT K%
3050 GOSUB 4410:'Fragen stellen
3060 OSTATUS%(N%)=I%
3070 GOTO 2580
3200 'Regeln auf stack legen
3210 '-> n% : Nummer der (Diagnose-)Regel
3220 IF STACK%=MSTACK% THEN CLS:PRINT"**** Stack-]berlauf ****":END
3230 STACK%=STACK%+1
3240 SRNR%(STACK%)=N%:PZAEHLER%(STACK%)=1
3250 RETURN
3400 'suchen der Objektnummer
3410 '->NAMEN$ : Name des Objektes
3420 '<-N%     : Nummer des Objektes
3430 FOR N%=1 TO OBJEKTE%
3440 IF OA$(N%)=NM$ THEN RETURN
3450 NEXT N%
3460 CLS
3470 PRINT"**** Objekt ";NM$;" nicht vorhanden ****"
3480 END
4000 'Diagnosen ausgeben
4010 ANZAHL%=0
4020 FOR N%=1 TO REGELN%
4030 IF (TYP%(N%)=1 AND RSTATUS%(N%)=1) THEN ANZAHL%=ANZAHL%+1:DIAG%=N%
4040 NEXT N%
4050 CLS
4060 PRINT @5,"D I A G N O S E N"
4070 IF ANZAHL%=0 THEN PRINT @256,"**** Es konnte keine Diagnose best{tigt werden ****":GOTO 4240
4080 IF ANZAHL%=1 THEN PRINT @256,"Best{tigte Diagnose : ";DANN$(DIAG%): GOTO 4240
4090 PRINT @128,"Best{tigte Diagnosen : "
4100 ZEILE%=10
4110 FOR N%=1 TO REGELN%
4120 IF (TYP%(N%)<>1 OR RSTATUS%(N%)<>1) GOTO 4210
4130 IF ZEILE%<18 THEN PRINT @ZEILE%*64+5,DANN$(N%):ZEILE%=ZEILE%+2:GOTO 4210
4140 PRINT @968,"Zur Fortsetzung eine Taste dr}cken";
4150 I$=INKEY$:IF I$="" THEN 4150
4160 CLS
4170 PRINT @5,"D I A G N O S E N"
4180 PRINT @128,"Fortsetzung best{tigte Diagnosen :"
4190 ZEILE%=10
4200 GOTO 4130
4210 NEXT N%
4220 GOTO 4240
4230 'Programmende
4240 PRINT @968,"Bitte eine Taste dr}cken : ";
4250 I$=INKEY$:IF I$="" THEN 4250
4260 CLS
4270 PRINT @5,"Was m|chten Sie ....."
4280 PRINT @133,"1. Programm beenden"
4290 PRINT @197,"2. Begr}ndung ausgeben"
4300 PRINT @261,"3. Neuer Durchlauf"
4310 PRINT 584,"Bitte w{hlen Sie : ";
4320 I$=INKEY$:IF I$="" THEN 4320
4330 PRINT I$:I%=VAL(I$)
4340 ON I% GOTO 4350,6010,280
4350 CLS:PRINT @5,"Programm beendet":PRINT:END
4370 GOTO 4270
4380 'Dialog-Komponente: Frage stellen
4390 '-> n% : Nr des unbestimmten Objektes
4400 '<- i% : Antwortcode
4410 FOR L%=1 TO FRAGEN%
4420 IF OA$(N%)=FOBJEKT$(L%) THEN 4520
4430 NEXT L%
4440 'keine Frage vorhanden
4450 CLS
4460 PRINT @128,"*** Es fehlt die Frage f}r '";OA$(N%);"' ***"
4470 PRINT @256,"Trifft diese Tatsache zu ?"
4480 PRINT @320,"Antwort  (Ja,Nein) : ";:INPUT I$
4490 IF (LEFT$(I$,1)="J" OR LEFT$(I$,1)="j") THEN I%=1:CLS:RETURN
4500 I%=(-1):CLS:RETURN
4510 'gefundene Frage nun stellen
4520 CLS
4530 PRINT @128,FTEXT$(L%)
4540 PRINT @256,"1 -  ";FA$(1,L%)
4550 PRINT @384,"2 -  ";FA$(2,L%)
4560 PRINT @968,"Welche Antwort ist korrekt ? (1,2) ";
4570 I$=INKEY$:IF I$="" THEN 4570
4580 PRINT I$
4590 IF I$="1" THEN I%=1:FSTATUS%(L%)=I%:CLS: RETURN
4600 IF I$="2" THEN I%=-1:FSTATUS%(L%)=I%:CLS:RETURN
4610 FOR I%=1 TO 5:CLS:PRINT @968,"*** Antwort ist falsch ***";:NEXT
4630 GOTO 4520
6000 'Begr}ndungskomponente
6010 CLS:PRINT"Begr}ndungs-Komponente ist nicht vorhanden":GOTO 4240
60000 'Definitionen
60050 CLS$=CHR$(26):WIDTH 255
60060 DEF FNLOCATE$(X,Y)=CHR$(27)+CHR$(61)+CHR$(32+Y)+CHR$(32+X)
60070 EIN$=CHR$(27)+CHR$(82):AUS$=CHR$(27)+CHR$(83):CLS$=CHR$(26)